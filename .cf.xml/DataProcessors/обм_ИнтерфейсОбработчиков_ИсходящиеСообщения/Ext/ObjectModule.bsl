#Область ПрограммныйИнтерфейс

Функция ВерсияИнтерфейса() Экспорт
	
	Возврат "250701";
	
КонецФункции

Функция РеализованИнтерфейс(пМетаданныеИнтерфейса) Экспорт
	
	Если пМетаданныеИнтерфейса = Метаданные.Обработки.обм_ИнтерфейсОбработчиков_ИсходящиеСообщения Тогда
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#Область ПереопределяемыйИнтерфейс

Функция ИсходящееСообщение_Отправить(пИсходящееСсылка, пУзелПолучатель) Экспорт
	
	Если РеализацияИнтерфейса = Неопределено Тогда
		ВызватьИсключение "Нет обработчика отправки исходящих сообщений";
		
	ИначеЕсли Не общ.ЭтоЗаполненнаяСсылка(пИсходящееСсылка, Тип("СправочникСсылка.обм_ИсходящиеСообщения")) Тогда
		ВызватьИсключение "Нет исходящего сообщения";
		
	ИначеЕсли ПустаяСтрока(обм.УзелПривести(пУзелПолучатель)) Тогда
		ВызватьИсключение "Не заполнен узел-получатель";
		
	КонецЕсли;

	результат = РеализацияИнтерфейса.ИсходящееСообщение_Отправить(пИсходящееСсылка, пУзелПолучатель);
	
	Возврат результат;
	
КонецФункции

Функция ИсходящееСообщение_Создать(пИсточникСсылка, пУзелОтправительИсточника = Неопределено) Экспорт
	
	ответПоУмолчанию = Неопределено;
	
	Если РеализацияИнтерфейса = Неопределено Тогда
		ВызватьИсключение "Нет обработчика создания исходящих сообщений";
		
	ИначеЕсли Не общ.ЭтоЗаполненнаяСсылка(пИсточникСсылка) Тогда
		ВызватьИсключение "Не заполнен источник данных для исходящего сообщения";
		
	КонецЕсли;

	обм.УзелПривести(пУзелОтправительИсточника);
	исходящее = РеализацияИнтерфейса.ИсходящееСообщение_Создать(пИсточникСсылка, пУзелОтправительИсточника);
	
	Если исходящее = Неопределено Тогда
		Возврат ответПоУмолчанию;
		
	КонецЕсли;
	
	исходящие = общ.Массив_ПоЗначению(исходящее, Тип("СправочникОбъект.обм_ИсходящиеСообщения"));
	
	Возврат исходящие;
	
КонецФункции

#КонецОбласти // ПереопределяемыйИнтерфейс

#КонецОбласти // ПрограммныйИнтерфейс
