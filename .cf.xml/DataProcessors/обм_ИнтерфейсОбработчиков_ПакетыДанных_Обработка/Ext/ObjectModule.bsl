#Область ПрограммныйИнтерфейс

Функция ВерсияИнтерфейса() Экспорт
	
	Возврат "251125";
	
КонецФункции

Функция РеализованИнтерфейс(пМетаданныеИнтерфейса) Экспорт
	
	Если пМетаданныеИнтерфейса = Метаданные.Обработки.обм_ИнтерфейсОбработчиков_ПакетыДанных_Обработка Тогда
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#Область ПереопределяемыйИнтерфейс

// Функция заполнения объекта-приемника данными заполнения
//
// Параметры:
//  пПриемникСсылка - ЛюбаяСсылка или Неопределено	 
//		- Ссылка на приемник, если он уже существует, иначе неопределено
//  пПакетДанных - СправочникСсылка.обм_ПакетыДанных
//		- Объект входящего запроса, содержащий данные для заполнения приемника 
//  ЗаписыватьПриемника	- Булево	 
//		- записать объект сразу при обработке
// 
// Возвращаемое значение:
// СсылочныйОбъект или Неопределено  - Заполненный объект при успехе обработки или неопределено при неудаче
//
Функция ПакетДанных_Обработать(пПакетДанных) Экспорт
	
	ответПоУмолчанию = Неопределено;
	
	// Значение по умолчанию
	Если РеализацияИнтерфейса = Неопределено Тогда
		Возврат ответПоУмолчанию;
		
	КонецЕсли;
	
	// Проверка годности параметров
	Если Не общ.ЭтоЗаполненнаяСсылка(пПакетДанных, Тип("СправочникСсылка.обм_ПакетыДанных")) Тогда
		Возврат ответПоУмолчанию;
		
	КонецЕсли;
	
	Попытка
		// Обработку пакета данных выполняем в транзакции, потому что если результат обработки пакета
		// не пройдет по условиям, надо будет отменить создание всех сопутствующих данных, которые были созданы
		// при обработке.
		НачатьТранзакцию();
		приемникОбъект = РеализацияИнтерфейса.ПакетДанных_Обработать(пПакетДанных);
		
		// Проверка приемника
		ОбъектСсылкаДругое = общ.ОбъектСсылкаДругое(приемникОбъект);

		Если ОбъектСсылкаДругое = "Объект" Тогда
			Если пПакетДанных.ОтказСозданияНовогоПриемника И приемникОбъект.ЭтоНовый() Тогда
				ОтменитьТранзакцию();
				Возврат ответПоУмолчанию;
				
			ИначеЕсли пПакетДанных.ОтказПерезаписиПриемника И Не приемникОбъект.ЭтоНовый() Тогда
				ОтменитьТранзакцию();
				Возврат ответПоУмолчанию;
				
			КонецЕсли;

		ИначеЕсли приемникОбъект = Неопределено Тогда
				// ОК
				
		Иначе
			комментОшибки = 
				СтрШаблон(
					"Обработчик заполнения приемника должен вернуть объект БД или 'Неопределено'
					|Возвращенное значение: '%1' типа '%2'
					|Объект входящего запроса: %3",
					приемникОбъект, // %1
					ТипЗнч(приемникОбъект), // %2
					пПакетДанных
					);
							
			ВызватьИсключение комментОшибки;
				
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		Возврат приемникОбъект;
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
		
	КонецПопытки;

	Возврат ответПоУмолчанию;
	
КонецФункции

// В процедуре Приемник_ПослеЗаписи выполнять проведение документов и обработку связанных с приемником данных
Процедура Приемник_ПослеЗаписи(пПриемник, пПакетДанных) Экспорт
	
	// Значение по умолчанию
	Если РеализацияИнтерфейса = Неопределено Тогда
		Возврат;
		
	КонецЕсли;
	
	РеализацияИнтерфейса.Приемник_ПослеЗаписи(пПриемник, пПакетДанных)
	
КонецПроцедуры

#КонецОбласти // ПереопределяемыйИнтерфейс

#КонецОбласти // ПрограммныйИнтерфейс
