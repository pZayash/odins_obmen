#Область ПрограммныйИнтерфейс

Функция ВерсияИнтерфейса() Экспорт
	
	Возврат "251125";
	
КонецФункции

Функция РеализованИнтерфейс(пМетаданныеИнтерфейса) Экспорт
	
	Если пМетаданныеИнтерфейса = Метаданные.Обработки.обм_ИнтерфейсОбработчиков_ВходящиеСообщение_ИзвлечениеПакетовДанных Тогда
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#Область ПереопределяемыйИнтерфейс

Функция ВходящееСообщение_Обрабатывается(пВходящееСообщениеСсылка) Экспорт
	
	ответПоУмолчанию = Ложь;
	
	// Значение по умолчанию
	Если РеализацияИнтерфейса = Неопределено Тогда
		Возврат ответПоУмолчанию;
		
	КонецЕсли;
	
	// Проверка годности параметров
	Если Не общ.ЭтоЗаполненнаяСсылка(пВходящееСообщениеСсылка, Тип("СправочникСсылка.обм_ВходящиеСообщения")) Тогда
		Возврат ответПоУмолчанию;
		
	КонецЕсли;
	
	сообщениеОбрабатывается = РеализацияИнтерфейса.ВходящееСообщение_Обрабатывается(пВходящееСообщениеСсылка);
			
	Если ТипЗнч(сообщениеОбрабатывается) <> Тип("Булево") Тогда
		сообщениеОбрабатывается = ответПоУмолчанию;
		
	КонецЕсли;
	
	Возврат сообщениеОбрабатывается;
	
КонецФункции

Функция ПакетыДанных_Извлечь(пВходящееСообщениеСсылка, пПримечаниеНаВозврат = "") Экспорт
	
	примечаниеПоУмолчанию = "Не обрабатывается";
	ответПоУмолчанию = Неопределено;
	
	// Значение по умолчанию
	Если РеализацияИнтерфейса = Неопределено Тогда
		пПримечаниеНаВозврат = примечаниеПоУмолчанию;
		Возврат ответПоУмолчанию;
		
	КонецЕсли;
	
	// Проверка годности параметров
	Если Не общ.ЭтоЗаполненнаяСсылка(пВходящееСообщениеСсылка, Тип("СправочникСсылка.обм_ВходящиеСообщения")) Тогда
		пПримечаниеНаВозврат = примечаниеПоУмолчанию;
		Возврат ответПоУмолчанию;
		
	КонецЕсли;
	
	пакетыДанных = РеализацияИнтерфейса.ПакетыДанных_Извлечь(пВходящееСообщениеСсылка, пПримечаниеНаВозврат);
	пПримечаниеНаВозврат = СокрЛП(пПримечаниеНаВозврат);
	
	Если ТипЗнч(пакетыДанных) = Тип("СправочникОбъект.обм_ПакетыДанных") Тогда
		промежуточныйМассив = Новый Массив;
		промежуточныйМассив.Добавить(пакетыДанных);
		пакетыДанных = промежуточныйМассив;
		
	КонецЕсли;
	
	Если ТипЗнч(пакетыДанных) <> Тип("Массив") Или пакетыДанных.Количество() = 0 Тогда
		Возврат ответПоУмолчанию;
		
	КонецЕсли;
	
	// Проверить данные заполнения, годные переложить в другой массив
	обработчикСсылка = Справочники.общ_Обработчики.НайтиСсылку(РеализацияИнтерфейса);
	годныеОбъекты = Новый Массив;
	Для Каждого пакетДанныхОбъект Из пакетыДанных Цикл
		Если ТипЗнч(пакетДанныхОбъект) <> Тип("СправочникОбъект.обм_ПакетыДанных") Тогда
			Продолжить;
			
		КонецЕсли;
		
		пакетДанныхОбъект.Владелец = пВходящееСообщениеСсылка;
		пакетДанныхОбъект.Обработчик = обработчикСсылка;
		
		годныеОбъекты.Добавить(пакетДанныхОбъект);
		
	КонецЦикла;
	пакетДанныхОбъект = Неопределено;

	Возврат годныеОбъекты;
	
КонецФункции 

#КонецОбласти // ПереопределяемыйИнтерфейс

#КонецОбласти // ПрограммныйИнтерфейс
