#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура Связать(пПриемник, пПакетДанных, пОбработчикЗагрузкиДанныхЗаполнения) Экспорт
	
	// Для контекстной подсказки
	Если Ложь Тогда
		пПакетДанных = Справочники.обм_ПакетыДанных.ПустаяСсылка();
		
	КонецЕсли;
	
	обработчикСсылка = Справочники.общ_Обработчики.НайтиСсылку(пОбработчикЗагрузкиДанныхЗаполнения);
	Если Не общ.ЭтоЗаполненнаяСсылка(обработчикСсылка, Тип("СправочникСсылка.общ_Обработчики")) Тогда
		Возврат;
		
	КонецЕсли;

	Если Не общ.ЭтоЗаполненнаяСсылка(пПриемник) Тогда
		Возврат;
		
	КонецЕсли; 
	
	Если Не общ.ЭтоЗаполненнаяСсылка(пПакетДанных, Тип("СправочникСсылка.обм_ПакетыДанных")) Или Не общ.Ссылка_Существует(пПакетДанных) Тогда
		Возврат;
		
	КонецЕсли;
	
	// Зафиксировать заполненного приемника в регистре
		// Получить последние данные
		запрос = Новый Запрос;
		запрос.УстановитьПараметр("пПриемник", пПриемник);
		запрос.УстановитьПараметр("пПакетДанных", пПакетДанных);
		запрос.УстановитьПараметр("обработчикСсылка", обработчикСсылка);
		запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПД.Ссылка КАК Ссылка,
		|	ПД.ДатаВремя КАК ДатаВремя,
		|	ПД.Данные_sha256 КАК Данные_sha256,
		|	ПД.Данные_Длина КАК Данные_Длина
		|ПОМЕСТИТЬ пакетДанных
		|ИЗ
		|	Справочник.обм_ПакетыДанных КАК ПД
		|ГДЕ
		|	ПД.Ссылка = &пПакетДанных
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВЫБОР
		|		КОГДА СвязьПП.Приемник ЕСТЬ НЕ NULL 
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ДанныеИзменены,
		|	пд.Ссылка КАК ПакетДанных,
		|	пд.ДатаВремя КАК ДатаВремя,
		|	пд.Данные_sha256 КАК Данные_sha256,
		|	пд.Данные_Длина КАК Данные_Длина
		|ИЗ
		|	пакетДанных КАК пд
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.обм_Приемники_ЗагруженныеПакетыДанных.СрезПоследних(, Приемник = &пПриемник) КАК СвязьПП
		|		ПО (СвязьПП.ОбработчикПакетаДанных <> &обработчикСсылка
		|				ИЛИ СвязьПП.ПакетДанных <> пд.Ссылка
		|				ИЛИ СвязьПП.ДатаВремя <> пд.ДатаВремя
		|				ИЛИ СвязьПП.Данные_sha256 <> пд.Данные_sha256
		|				ИЛИ СвязьПП.Данные_Длина <> пд.Данные_Длина)";
		выборка = запрос.Выполнить().Выбрать();
		
		Если Не выборка.Следующий() Тогда 
			Возврат;
			
		КонецЕсли;
		
		Если Не выборка.ДанныеИзменены Тогда
			Возврат;
			
		КонецЕсли;
		
		запись = РегистрыСведений.обм_Приемники_ЗагруженныеПакетыДанных.СоздатьМенеджерЗаписи();
		запись.Период = общ.ДатаУниверсальная();
		запись.Приемник = пПриемник;
		запись.ПакетДанных = пПакетДанных;
		запись.ДатаВремя = выборка.ДатаВремя;
		запись.Данные_sha256 = выборка.Данные_sha256;
		запись.Данные_Длина = выборка.Данные_Длина; 
		запись.ОбработчикПакетаДанных = обработчикСсылка;
		запись.Записать(); 
	
КонецПроцедуры
	
#КонецЕсли // #Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

