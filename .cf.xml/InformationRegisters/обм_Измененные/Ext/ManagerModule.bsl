Процедура Зарегистрировать(пОбъект, Знач пДополнительныеСвойства = Неопределено) Экспорт

	ссылкаОбъекта = общ.Ссылка_Получить(пОбъект);

	Если ссылкаОбъекта = Неопределено Тогда
		Возврат;
		
	КонецЕсли;
	
	запись = РегистрыСведений.обм_Измененные.СоздатьМенеджерЗаписи();

	запись.Ссылка = ссылкаОбъекта;
	запись.ДатаВремя = общ_Сервер.ТекущаяДатаНаСервере();
	запись.ВерсияДанных = ссылкаОбъекта.ВерсияДанных;
	
	объектСсылкаДругое = Общ.ОбъектСсылкаДругое(пОбъект);
	
	Если объектСсылкаДругое = "Объект" И ТипЗнч(пДополнительныеСвойства) <> Тип("Структура") Тогда
		пДополнительныеСвойства = пОбъект.ДополнительныеСвойства;
		
	ИначеЕсли ТипЗнч(пДополнительныеСвойства) <> Тип("Структура") Тогда
		пДополнительныеСвойства = Новый Структура;
		
	КонецЕсли;
	
	пДополнительныеСвойства.Свойство("УзелОтправитель", запись.УзелОтправитель);
	пДополнительныеСвойства.Свойство("Категория", запись.Категория);
	пДополнительныеСвойства.Свойство("Идентификатор", запись.Идентификатор);
	
	УстановитьПривилегированныйРежим(Истина);
	запись.Записать(Истина);
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура УдалитьРегистрацию(пОбъект, пВерсияДанных = Неопределено) Экспорт

	объектСсылка = общ.Ссылка_Получить(пОбъект);

	Если объектСсылка = Неопределено Тогда
		Возврат;
		
	КонецЕсли;
	
	записьРегистраИзмененные = РегистрыСведений.обм_Измененные.СоздатьМенеджерЗаписи();
	записьРегистраИзмененные.Ссылка = объектСсылка;
	
	// Контроль версии данных, если она передана в параметре
	Если Не ПустаяСтрока(пВерсияДанных) Тогда
		записьРегистраИзмененные.Прочитать();
		
		// Удалять только если версия данных не изменилась
		Если Не записьРегистраИзмененные.Выбран() Или записьРегистраИзмененные.ВерсияДанных <> пВерсияДанных Тогда
			Возврат;
			
		КонецЕсли; // пВерсияДанных <> Неопределено
		
	КонецЕсли; // записьРегистраИзмененные.ВерсияДанных <> пВерсияДанных
	
	записьРегистраИзмененные.Удалить();
	
КонецПроцедуры