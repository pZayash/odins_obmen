#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УправлениеДиалогом();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КОтправке(Команда)
	
	ключиЗаписей = Элементы.Список.ВыделенныеСтроки;

	Если ключиЗаписей.Количество() = 0 Тогда
		Возврат;
		
	КонецЕсли;

	КОтправкеНаСервере(ключиЗаписей);
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура КОтправкеСУдалениемРегистрации(Команда)
	
	ключиЗаписей = Элементы.Список.ВыделенныеСтроки;

	Если ключиЗаписей.Количество() = 0 Тогда
		Возврат;
		
	КонецЕсли;
	
	удалитьРегистрацию = Истина;
	КОтправкеНаСервере(ключиЗаписей, удалитьРегистрацию);
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура КОтправкеНаСервере(Знач пКлючиЗаписей, Знач пУдалитьРегистрацию = Ложь)

	Для Каждого ключ Из пКлючиЗаписей Цикл
		РегистрыСведений.обм_Измененные.КОтправке(ключ.Ссылка, , ключ.УзелОтправитель, пУдалитьРегистрацию); 
		
	КонецЦикла;
	ключ = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключитьРегламентное(Команда)
	
	ПереключитьРегламентноеНаСервере();
	
КонецПроцедуры 

&НаСервере
Процедура ПереключитьРегламентноеНаСервере()
	
	регламентное = Регламентное_Найти();
	
	использование = регламентное.Использование;
	
	регламентное.Использование = Не использование;
	регламентное.Записать();
	
	УправлениеДиалогом();

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьРазмерОчереди(Команда)
	
	размерОчереди = РазмерОчередиНаСервере();
	
	текстЗаголовка = СтрШаблон("%1 Размер очереди %2. Нажмите здесь чтобы пересчитать",
		Формат(ТекущаяДата(), "ДФ=HH:mm:ss"), // %1
		размерОчереди 		);
	
	Элементы.ПоказатьРазмерОчереди.Заголовок = текстЗаголовка;
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервере
Функция РазмерОчередиНаСервере()

	размерОчереди = общ.ТаблицаФормыДинамическийСписок_Количество(ЭтаФорма.Элементы["Список"]);
	
	Возврат размерОчереди;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция Регламентное_Найти()
	мета = Метаданные.РегламентныеЗадания.обм_Отправка_1_ИсходящиеСообщения_Создать;
	регламентное = РегламентныеЗадания.НайтиПредопределенное(мета);
	
	Возврат РегламентныеЗадания.НайтиПредопределенное(регламентное);
	
КонецФункции

&НаСервере
Процедура УправлениеДиалогом()
	
	Если Элементы.Список.Видимость Тогда
		Элементы.Список.Обновить();
		
	КонецЕсли; 
	
	Если Элементы.ФормаПереключитьРегламентное.Видимость Тогда
		регламентное = Регламентное_Найти();
		
		заголовокКнопки = Неопределено;
		картинкаКнопки = Неопределено;
		Если регламентное.Использование Тогда
			заголовокКнопки = "Задание работает";
			картинкаКнопки = БиблиотекаКартинок.ОформлениеКругЗеленый;
			
		Иначе
			заголовокКнопки = "Задание отключено";
			картинкаКнопки = БиблиотекаКартинок.ОформлениеКругПустой;
			
		КонецЕсли;

		Элементы.ФормаПереключитьРегламентное.Заголовок = заголовокКнопки;
		Элементы.ФормаПереключитьРегламентное.Картинка = картинкаКнопки;
					
	КонецЕсли; 		

КонецПроцедуры

#КонецОбласти