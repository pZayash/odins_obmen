#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
// @TODO 250721 Заяш: Убрать лишние методы из программного интерфейса
#Область ПрограммныйИнтерфейс

Функция ИсходящееСообщение_Отправить(пИсходящееСообщение, пУзел, пОбработчикОтправки) Экспорт
	
	результат = Неопределено;
	
	вызыватьОшибку = Истина;
	РегистрыСведений.обм_КОтправке.ПроверитьВекторИсходящееУзелОбработчик(
		пИсходящееСообщение, пУзел, пОбработчикОтправки, вызыватьОшибку);

	имяСобытия = обм.ИмяСобытияЖурналаРегистрации("Отправка.ИсходящееСообщение_Отправить");

	ссылкиПараметров = Новый Массив;
	ссылкиПараметров.Добавить(пИсходящееСообщение);
	ссылкиПараметров.Добавить(пОбработчикОтправки);
	
	НачатьТранзакцию();
	Попытка
		интерфейс = Обработки.обм_ИнтерфейсОбработчиков_ИсходящиеСообщения_СозданиеАдресацияОтправка.Создать();
		обработчик = Справочники.общ_Обработчики.ОбработчикОдиночка(пОбработчикОтправки, пОбработчикОтправки.ВерсияДанных);
		общ.Интерфейс_УстановитьРеализацию(интерфейс, обработчик);
		результат = интерфейс.ИсходящееСообщение_Отправить(пИсходящееСообщение, пУзел);
			
		записьОбОтправке = РегистрыСведений.обм_ОтправленныеСообщения.СоздатьМенеджерЗаписи();
		записьОбОтправке.ИсходящееСообщение = пИсходящееСообщение;
		записьОбОтправке.ОбработчикОтправки = Справочники.общ_Обработчики.НайтиСсылку(обработчик);
		записьОбОтправке.Узел = пУзел;
		записьОбОтправке.ДатаОтправки = общ.ДатаУниверсальная();
		записьОбОтправке.Записать();
		
		// В журнале регистрации сделать запись о выгрузке объекта 
		комментарийРезультата = 
			СтрШаблон(
				"Объект %1 выгружен в узел: %2
				|обработчиком: %3",
				пИсходящееСообщение, // %1
				пУзел, // %2
				пОбработчикОтправки // %3
				);
				
		общ.ВЖурнал(имяСобытия, комментарийРезультата, , ссылкиПараметров);
		
		ЗафиксироватьТранзакцию();
		
		Возврат результат;

	Исключение
		ОтменитьТранзакцию();
		инфоОшибки = ИнформацияОбОшибке();
		комментарийРезультата = общ.Ошибка_ПодробноеПредставление(инфоОшибки);
		общ.Ошибка_ВЖурналРегистрации(имяСобытия, комментарийРезультата, ссылкиПараметров);
		
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецФункции

Процедура ПоднятьФлагОшибкиОтправки(пИсходящееСообщение, пУзел, пОбработчикОтправки) Экспорт
	
	РегистрыСведений.обм_КОтправке.Зарегистрировать(пИсходящееСообщение, пУзел, пОбработчикОтправки, Истина);
	
КонецПроцедуры

Процедура СброситьФлагиОшибокОтправки() Экспорт
	
	запрос = Новый Запрос;
	запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КО.ИсходящееСообщение КАК ИсходящееСообщение,
	|	КО.Узел КАК Узел,
	|	КО.ОбработчикОтправки КАК ОбработчикОтправки
	|ИЗ
	|	РегистрСведений.обм_КОтправке КАК КО
	|ГДЕ
	|	КО.ФлагОшибкиОтправки";
	
	выборка = запрос.Выполнить().Выбрать();
	имяТаблицы = общ.Объект_ИмяТаблицы(Метаданные.РегистрыСведений.обм_КОтправке);
	
	Пока выборка.Следующий() Цикл
		Попытка
			РегистрыСведений.обм_КОтправке.Зарегистрировать(выборка.ИсходящееСообщение, выборка.Узел, выборка.ОбработчикОтправки, Ложь);
			
		Исключение
			// При исключении не прервывать обработку, только записать ошибку в журнал регистрации
			// оставшийся флаг ошибки отправки не серьезная проблема, гораздо хуже если из-за
			// облома процесса остануться висеть все флаги ошибок
			имяСобытия = обм.ИмяСобытияЖурналаРегистрации("Отправка.СброситьФлагиОшибокОтправки");
			инфоОшибки = ИнформацияОбОшибке();
			ссылкиДанных = Новый Массив;
			ссылкиДанных.Добавить(выборка.ИсходящееСообщение);
			ссылкиДанных.Добавить(выборка.ОбработчикОтправки);
			общ.Ошибка_ВЖурналРегистрации(имяСобытия, инфоОшибки, ссылкиДанных);
				
		КонецПопытки;

	КонецЦикла;
	выборка.Сбросить();
	
КонецПроцедуры

Процедура УдалитьРегистрацию(пИсходящееСообщение, пУзел, пОбработчикОтправки) Экспорт
	
	// Проверить заполненность вектора
	Если Не ПроверитьВекторИсходящееУзелОбработчик(пИсходящееСообщение, пУзел, пОбработчикОтправки) Тогда
		Возврат;
		
	КонецЕсли;
	
	блокировка = Новый БлокировкаДанных;
	элементБлок = блокировка.Добавить(общ.Объект_ИмяТаблицы(Метаданные.РегистрыСведений.обм_КОтправке));
	элементБлок.Режим = РежимБлокировкиДанных.Исключительный;
	элементБлок.УстановитьЗначение("ИсходящееСообщение", пИсходящееСообщение);
	элементБлок.УстановитьЗначение("ОбработчикОтправки", пОбработчикОтправки);
	элементБлок.УстановитьЗначение("Узел", пУзел);
	
	Попытка
		НачатьТранзакцию();
		блокировка.Заблокировать();
		запись = РегистрыСведений.обм_КОтправке.СоздатьМенеджерЗаписи();
		запись.ИсходящееСообщение = пИсходящееСообщение;
		запись.ОбработчикОтправки = пОбработчикОтправки;
		запись.Узел = пУзел;
		
		запись.Прочитать(); 
	
		Если запись.Выбран() Тогда
			запись.Удалить();
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
		
	КонецПопытки;

КонецПроцедуры

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныйПрограммныйИнтерфейс

Процедура Зарегистрировать(пИсходящееСообщение, пУзел, пОбработчикОтправки, пФлагОшибки = Ложь) Экспорт  

	// Проверить заполненность вектора
	ПроверитьВекторИсходящееУзелОбработчик(пИсходящееСообщение, пУзел, пОбработчикОтправки, Истина);
	
	блокировка = Новый БлокировкаДанных;
	элементБлок = блокировка.Добавить(общ.Объект_ИмяТаблицы(Метаданные.РегистрыСведений.обм_КОтправке));
	элементБлок.Режим = ?(пФлагОшибки, РежимБлокировкиДанных.Исключительный, РежимБлокировкиДанных.Разделяемый);
	элементБлок.УстановитьЗначение("ИсходящееСообщение", пИсходящееСообщение);
	элементБлок.УстановитьЗначение("ОбработчикОтправки", пОбработчикОтправки);
	элементБлок.УстановитьЗначение("Узел", пУзел);
	
	Попытка
		НачатьТранзакцию();
		блокировка.Заблокировать();
		запись = РегистрыСведений.обм_КОтправке.СоздатьМенеджерЗаписи();
		запись.ИсходящееСообщение = пИсходящееСообщение;
		запись.ОбработчикОтправки = пОбработчикОтправки;
		запись.Узел = пУзел;
		запись.ДатаВремя = общ.ДатаУниверсальная();
		запись.ФлагОшибкиОтправки = пФлагОшибки;
		
		запись.Записать(Истина); 
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
		
	КонецПопытки;

КонецПроцедуры

Функция ОбработатьОчередьОтправки() Экспорт 
	
	// Сбросить флаги ошибок прошлой отправки
	РегистрыСведений.обм_КОтправке.СброситьФлагиОшибокОтправки();
	
	Пока Истина Цикл
		
		запрос = Новый Запрос;
		запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 100
		|	КО.ИсходящееСообщение КАК ИсходящееСообщение,
		|	КО.ОбработчикОтправки КАК ОбработчикОтправки,
		|	КО.Узел КАК Узел
		|ИЗ
		|	РегистрСведений.обм_КОтправке КАК КО
		|ГДЕ
		|	КО.ФлагОшибкиОтправки = ЛОЖЬ";
		
		выборка = запрос.Выполнить().Выбрать();
		
		Если выборка.Количество() = 0 Тогда
			Прервать;
			
		КонецЕсли;
		
		Пока выборка.Следующий() Цикл
			
			узел = выборка.Узел;
			обм.УзелПривести(узел);
			
			Попытка
				РегистрыСведений.обм_КОтправке.ИсходящееСообщение_Отправить(выборка.ИсходящееСообщение, узел, выборка.ОбработчикОтправки);
				РегистрыСведений.обм_КОтправке.УдалитьРегистрацию(выборка.ИсходящееСообщение, узел, выборка.ОбработчикОтправки);

			Исключение
				инфоОшибки = ИнформацияОбОшибке();
				подробно = общ.Ошибка_ПодробноеПредставление(инфоОшибки);
				общ.Ошибка_ВЖурналРегистрации(обм.ИмяСобытияЖурналаРегистрации("Отправка.ОбработкаОчереди"), подробно);
					
				РегистрыСведений.обм_КОтправке.ПоднятьФлагОшибкиОтправки(выборка.ИсходящееСообщение, узел, выборка.ОбработчикОтправки);
				
			КонецПопытки;
			
			
		КонецЦикла;
		выборка = Неопределено;
		
	КонецЦикла; // Пока Истина

КонецФункции

Функция ПроверитьВекторИсходящееУзелОбработчик(пИсходящееСообщение, пУзел, пОбработчикОтправки, пВызватьИсключание = Ложь) Экспорт

	результатыПроверки = Новый Массив;
	
	// Проверить заполненность вектора
	Если Не общ.ЭтоЗаполненнаяСсылка(пИсходящееСообщение, Тип("СправочникСсылка.обм_ИсходящиеСообщения")) Тогда
		результатыПроверки.Добавить("Не заполнено исходящее сообщение;");
		
	КонецЕсли;
	
	Если ПустаяСтрока(пУзел) Тогда
		результатыПроверки.Добавить("Не заполнен узел-получатель;");
		
	КонецЕсли;
	
	Если Не Общ.ЭтоЗаполненнаяСсылка(пОбработчикОтправки, Тип("СправочникСсылка.общ_Обработчики")) Тогда
		результатыПроверки.Добавить("Не заполнен обработчик отправки;");
		
	КонецЕсли;
	
	Если результатыПроверки.Количество() = 0 Тогда
		Возврат Истина;
		
	КонецЕсли;
	
	комментарийПроверки = СтрСоединить(результатыПроверки, Символы.ПС);
	
	Если ПустаяСтрока(комментарийПроверки) Тогда
		Возврат Истина;
		
	ИначеЕсли пВызватьИсключание Тогда
		ВызватьИсключение комментарийПроверки;
		
	Иначе
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти // СлужебныйПрограммныйИнтерфейс

#КонецЕсли // #Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
