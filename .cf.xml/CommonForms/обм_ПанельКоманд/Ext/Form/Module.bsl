&НаКлиенте
Перем ПараметрыВыполненияКоманды;

#Область СлужебныйПрограммныйИнтерфейс

&НаКлиенте
Процедура ПараметрыВыполненияКоманды_Установить(пПараметрыВыполненияКоманды) Экспорт
	
	ПараметрыВыполненияКоманды = пПараметрыВыполненияКоманды;
	
КонецПроцедуры

#КонецОбласти // СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Для ИР
	Если Параметры.Свойство("АвтоТест") Тогда 
		Возврат; КонецЕсли;
	
	доступенПросмотрИнтерфейсаПодсистемыОбмена = ПравоДоступа("Просмотр", Метаданные.Подсистемы.обм_Интерфейсная);
	
	Если Не доступенПросмотрИнтерфейсаПодсистемыОбмена Тогда
		Отказ = Истина;
		Возврат;
		
	КонецЕсли;
	
	ПараметрыИсточника = Новый Структура("ВыделенныеСтроки, ИмяФормы, Источник, СсылкиИсточников");
	ЗаполнитьЗначенияСвойств(ПараметрыИсточника, Параметры);
	
	// Заголовок окна
	ЭтаФорма.Заголовок = "";
	
	текстНадписи = "Команды обмена";
	Если ПараметрыИсточника.Источник <> Неопределено Тогда
		текстНадписи = "Команды обмена для " + ПараметрыИсточника.Источник;
		
	КонецЕсли;
	
	Если общ.ЭтоЗаполненныйМассив(ПараметрыИсточника.ВыделенныеСтроки) Тогда
		всегоОбъектов = ПараметрыИсточника.ВыделенныеСтроки.Количество();
		текстНадписи = СтрШаблон(	"%1
									|и других, всего %2 об-т",
									текстНадписи, // %1
									всегоОбъектов		);
		
	КонецЕсли;
	
	Элементы.надписьЗаголовок.Заголовок = текстНадписи;
	
	Если общ.ЭтоЗаполненнаяСсылка(ПараметрыИсточника.Источник) Тогда
		Если ПараметрыИсточника.ВыделенныеСтроки <> Неопределено Тогда
			ПараметрыИсточника.СсылкиИсточников = ПараметрыИсточника.ВыделенныеСтроки;
			
		Иначе 
			ПараметрыИсточника.СсылкиИсточников.Добавить(ПараметрыИсточника.Источник);
			
		КонецЕсли;
		
		Элемент_Вставить("Приемник_ЗагруженныеПакетыДанных", "Показать пакеты данных приемника");
		Элемент_Вставить("Приемники_ОбновитьПоследнимПакетомДанных", "Обновить последним пакетом данных");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НажатиеДекорации(Элемент)
	
	ЗакрытьФорму();

	// По умолчанию вызвать метод из общего модуля команд
	// BSLLS:Typo-off
	
	вызов = "обм_командыКлиент." + ТекущийЭлемент.Имя + "(ПараметрыИсточника.СсылкиИсточников, ПараметрыВыполненияКоманды)";
	Выполнить(вызов);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция Элемент_Вставить(пИмяЭлемента, пЗаголовок = "", пТипЭлемента = Неопределено) Экспорт
	
	элементРезультат = Элементы.Найти(пИмяЭлемента);
	
	Если ПустаяСтрока(пЗаголовок) Тогда
		пЗаголовок = пИмяЭлемента;
		
	КонецЕсли;
	
	Если элементРезультат <> Неопределено Тогда
		Возврат элементРезультат;
		
	КонецЕсли;

	// По умолчанию вставлять декорацию
	Если пТипЭлемента = Неопределено Тогда
		пТипЭлемента = Тип("ДекорацияФормы");
		
	КонецЕсли;
	
	элементРезультат = Элементы.Добавить(пИмяЭлемента, пТипЭлемента, Элементы.МестоВставки);
	элементРезультат.Заголовок = пЗаголовок;
	
	Если пТипЭлемента = Тип("ДекорацияФормы") Тогда
		элементРезультат.УстановитьДействие("Нажатие", "НажатиеДекорации");
		элементРезультат.Гиперссылка = Истина;
		
	КонецЕсли;
	
	Если пТипЭлемента = Тип("ПолеФормы") Тогда
		элементРезультат.Вид = ВидПоляФормы.ПолеВвода;
		
	КонецЕсли;
	
	Если ТипЗнч(элементРезультат) = Тип("ПолеФормы") ИЛИ ТипЗнч(элементРезультат) = Тип("ТаблицаФормы") Тогда
		элементРезультат.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
		
	КонецЕсли;
	
	Возврат элементРезультат;
	
КонецФункции

&НаКлиенте
Процедура ЗакрытьФорму(Команда = Неопределено)
	
	ЭтаФорма.Закрыть();
	
КонецПроцедуры

#КонецОбласти