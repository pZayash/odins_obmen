#Область ПрограммныйИнтерфейс

Функция Одиночка(пОбработка, пВерсияДанных = Неопределено) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	обработчикОбъект = Справочники.общ_Обработчики.ОбработчикОбъект(пОбработка);
		
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат обработчикОбъект;

КонецФункции

// Получает уникальный идентификатор информационной базы,
// с помощью которого можно различать различные экземпляры информационных баз,
// например, при сборе статистики или в механизмах внешнего управления базами.
// Если идентификатор не заполнен, то его значение автоматически устанавливается и возвращается.
//
// Идентификатор хранится в константе общ_ИдентификаторИнформационнойБазы.
// Константа общ_ИдентификаторИнформационнойБазы не должна входить в составы планов обмена, чтобы иметь
// различные значения в каждой информационной базе (узле РИБ).
//
// Возвращаемое значение:
//  Строка - идентификатор информационной базы.
//
Функция ИдентификаторИнформационнойБазы() Экспорт
	
	ид = Константы.общ_ИдентификаторИнформационнойБазы.Получить();
	
	// Проверка, что строка соединения информационной базы не изменилась
	// ид имеет вид структурированного имени файла (см. общ.Файл_ИмяДополненноеИдентификатором)
	// и первая часть соответствует строке соединения базы. Сравнить ее с текущей строкой
	параметрыИд = общ.Файл_ПараметрыИмени(ид);
	
	частичноеИмяИБ = общ.Файл_ПодготовитьИмя(СтрокаСоединенияИнформационнойБазы());
	частичноеИмяИБ = СтрЗаменить(частичноеИмяИБ, ".", "_");
	частичноеИмяИБ = СокрЛП(НРег(частичноеИмяИБ));
	
	идГодный = Не параметрыИд.Отказ И параметрыИд.Имя = частичноеИмяИБ;

	Если идГодный Тогда
		Возврат ид;
		
	КонецЕсли;
	
	// Если ид плохой, сформировать новый
	уид = Строка(Новый УникальныйИдентификатор());
	новыйИд = общ.Файл_ИмяДополненноеИдентификатором(частичноеИмяИБ, уид, "dt");
	новыйИд = НРег(новыйИд);
		
	УстановитьОтключениеБезопасногоРежима(Истина);
	УстановитьПривилегированныйРежим(Истина);
		
	Константы.общ_ИдентификаторИнформационнойБазы.Установить(новыйИд);
		
	УстановитьПривилегированныйРежим(Ложь);
	УстановитьОтключениеБезопасногоРежима(Ложь);

	Возврат новыйИд;
	
КонецФункции

Функция ИдентификаторСеанса() Экспорт
	
	Возврат Новый УникальныйИдентификатор;
	
КонецФункции

Функция обм_ИмяПодсистемы() Экспорт

	Возврат Метаданные.Подсистемы.Общ.Подсистемы.обм_ОбменДанными.Имя;
	
КонецФункции

Функция ЦветОтрицательный() Экспорт

	// Цвет указал явно, т.к. с веб-цветом случается ошибка передачи с клиента на сервер
	Возврат Новый Цвет(255, 140, 105); // Лососёвый / #ff8c69
	
КонецФункции

Функция ЦветПоложительный() Экспорт

	// Цвет указал явно, т.к. с веб-цветом случается ошибка передачи с клиента на сервер
	Возврат Новый Цвет(0, 255, 127); // Springgreen / Зелёная весна / #00ff7f
	
КонецФункции

// Чтобы можно было отличить тестовую базу от боевой
//
Функция ЭтоТестоваяБаза() Экспорт  
	
	Результат = Ложь;
	
	СтрокаСоединениеИнфоБазы = ВРег(СтрокаСоединенияИнформационнойБазы());

	МассивСловОбозначающихКопию = Новый  Массив;
	МассивСловОбозначающихКопию.Добавить("COPY");
	МассивСловОбозначающихКопию.Добавить("DEV");
	МассивСловОбозначающихКопию.Добавить("TEST");
	МассивСловОбозначающихКопию.Добавить("TST");
	МассивСловОбозначающихКопию.Добавить("КОПИЯ");
	МассивСловОбозначающихКопию.Добавить("РАЗРАБОТКА");
	МассивСловОбозначающихКопию.Добавить("ТЕСТ");
	МассивСловОбозначающихКопию.Добавить("ТСТ");
	
	Для Каждого Слово Из МассивСловОбозначающихКопию Цикл
		Если Найти(СтрокаСоединениеИнфоБазы, Слово) Тогда
			Результат = Истина;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	Слово = Неопределено;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти // Программный интерфейс
