Асинх Функция ВыбратьКаталог(пЗаголовок = "", пКаталог = "") Экспорт
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	Диалог.МножественныйВыбор = Ложь; 
	
	Если Не ПустаяСтрока(пКаталог) Тогда
		Диалог.Каталог = СокрЛП(пКаталог);
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(пЗаголовок) Тогда
		Диалог.Заголовок = пЗаголовок;
		
	КонецЕсли;
		
	Обещание = Диалог.ВыбратьАсинх();
    МассивКаталогов = Ждать Обещание; 
	
	Если типЗнч(МассивКаталогов) = Тип("Массив") И МассивКаталогов.Количество() > 0 Тогда
   		Возврат МассивКаталогов[0];
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Процедура Документы_ОбработатьУведомитьПользователя(массивСсылокДокументовКОбработке, ВидОбработки, УведомитьОРезультате = Ложь) Экспорт
	
	// Проверить вид обработки
	Если Не общ.Документы_ВидОбработки_Проверить(ВидОбработки) Тогда
		Возврат;
		
	КонецЕсли;
	
	// Сортировать массив документов
	массивСсылокДокументовКОбработке = 
		общ.Документы_СортироватьПоДате(
			массивСсылокДокументовКОбработке, 
	       	?(ВидОбработки = "ОтменаПроведения", "Убыв", "Возр")
			);
	
	ВсегоДокументов = массивСсылокДокументовКОбработке.Количество();
	ОбработаноДокументов = 0;
	КоличествоОшибок = 0;
	стрРезультатПроведения = ""; 
	
	времяНачала = ТекущаяДата();
	следующееОповещение = ТекущаяДата();
	
	видОбработки_Представление = общ.Документы_ВидОбработки_Представление(ВидОбработки);
	
	Для Каждого ДокументСсылка Из массивСсылокДокументовКОбработке Цикл
		ОбработкаПрерыванияПользователя();
		
		// Сбросить описание ошибки
		стрОписаниеОшибки = "";

		стрОписаниеОшибки = общ.Документ_Обработать(ДокументСсылка, ВидОбработки);
		
		Если ЗначениеЗаполнено(стрОписаниеОшибки) Тогда
			Сообщ = Новый СообщениеПользователю;
			Сообщ.Текст = стрОписаниеОшибки;
			Сообщ.Сообщить();
			
		КонецЕсли; 
		
		ОбработаноДокументов = ОбработаноДокументов + 1;
		этоПервый = ОбработаноДокументов = 1;
		этоПоследний = ОбработаноДокументов >= ВсегоДокументов;
		// Оповещение о прогрессе
		Если ТекущаяДата() > следующееОповещение Или этоПервый Или этоПоследний Тогда
			времяОбработкиСек = Макс(ТекущаяДата() - времяНачала, 1);
			скоростьОбработки = Окр(60*ОбработаноДокументов/времяОбработкиСек, 2);
			
			стрРезультатПроведения = 
				СтрШаблон(
					"%1: %2/%3 %4
					|прошло %6:%7, %5/мин",
					видОбработки_Представление,
					Формат(ОбработаноДокументов,"ЧГ=0"), // %2
					Формат(ВсегоДокументов,"ЧГ=0"), // %3
					?(КоличествоОшибок = 0, "", " Ошибок " + Формат(КоличествоОшибок,"ЧГ=0")), // %4
				    Формат(скоростьОбработки,"ЧГ=0"), // %5 Скорость
					Формат(Окр(времяОбработкиСек/60, 0),"ЧН=; ЧГ=0"), // %6 Время обработки мин
					Формат(Окр(времяОбработкиСек%60, 0),"ЧН=; ЧЦ=2; ЧВН=; ЧГ=0") // %7 Время обработки сек
					); 
					
			массивОПроведении = СтрРазделить(стрРезультатПроведения, Символы.ПС);
					
			Состояние(
				массивОПроведении[0], // Состояние 
				100*ОбработаноДокументов/Макс(ВсегоДокументов, 1), // Прогресс
				массивОПроведении[1] // Пояснение
				); 
				
			// Оповещать раз в три секунд
			следующееОповещение = ТекущаяДата() + 3; 
			
		КонецЕсли; // Если ТекущаяДата() > следующееОповещение

	КонецЦикла;
	ДокументСсылка = Неопределено;  
	
	Если Не ПустаяСтрока(стрРезультатПроведения) Тогда
		Сообщ = Новый СообщениеПользователю;
		Сообщ.Текст = стрРезультатПроведения;
		Сообщ.Сообщить();

	КонецЕсли;

КонецПроцедуры

// Процедура устанавливает отбор СКД. Используется для СКД и отборов динамических списков
//
// Параметры:
//		ОтборСКД
//			Обязательный, отбор СКД или группа элементов отбора компоновки данных
//			Отбор, в котором необходимо установить элемент
//		ЛевоеЗначение
//			Обязательный, Строка или ПолеКомпоновкиДанных
//			Поле, по которому будет происходить отбор
//		ПравоеЗначение
//			Обязательный, произвольный
//			Значение для отбора
//		ВидСравненияСКД
//			Необязательный, ВидСравненияКомпоновкиДанных
//			По умолчанию неопределено
//			Если значение не заполнено, будет взят вид сравнения "Равно".
//		Использование
//			Необязательный, Булево
//			По умолчанию Истина
//			Признак, использовать параметр или нет
// 		ЭлементОтбораДанных
//			Необязательный, не заполнять
//			Нужен для рекурсивной работы процедуры
//
// 20120905103206 Заяш
// 
Процедура СКД_Отбор_Установить(
		ОтборСКД, 
		знач ЛевоеЗначение, 
		знач ПравоеЗначение, 
		ВидСравненияСКД = Неопределено, 
		Использование = Истина, 
		ЭлементОтбораДанных = Неопределено
		) Экспорт
	
	Если ТипЗнч(ЛевоеЗначение) = Тип("Строка") Тогда
		ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ЛевоеЗначение);
	КонецЕсли;
	   
	Если ВидСравненияСКД = Неопределено Тогда
		ВидСравненияСКД = ВидСравненияКомпоновкиДанных.Равно;
	КонецЕсли;
	
	ЭлементыОтбора = ОтборСКД.Элементы;
	
	Для Каждого ЭлементОтбора Из ЭлементыОтбора Цикл
		
		Если ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			// Вызвать рекурсивно эту процедуру
			СКД_Отбор_Установить(ЭлементОтбора, ЛевоеЗначение, ПравоеЗначение, ВидСравненияСКД, Использование, ЭлементОтбораДанных);
			Продолжить;
		КонецЕсли;
		
		Если ЭлементОтбора.ЛевоеЗначение = ЛевоеЗначение Тогда
			Если ЭлементОтбораДанных = Неопределено Тогда
				ЭлементОтбораДанных = ЭлементОтбора;
				ЭлементОтбораДанных.ВидСравнения = ВидСравненияСКД;
				ЭлементОтбораДанных.ПравоеЗначение = ПравоеЗначение;
				ЭлементОтбораДанных.Использование = Использование;
			Иначе
				ЭлементОтбора.Использование = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// Если элемент отбора не найден, добавить новый в корень и установить ему значения
	Если ЭлементОтбораДанных = Неопределено И ТипЗнч(ОтборСКД) = Тип("ОтборКомпоновкиДанных") Тогда
		ЭлементОтбораДанных = ЭлементыОтбора.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбораДанных.ЛевоеЗначение = ЛевоеЗначение;
		ЭлементОтбораДанных.ВидСравнения = ВидСравненияСКД;
		ЭлементОтбораДанных.ПравоеЗначение = ПравоеЗначение;
		ЭлементОтбораДанных.Использование = Использование;
	КонецЕсли;
	
КонецПроцедуры

