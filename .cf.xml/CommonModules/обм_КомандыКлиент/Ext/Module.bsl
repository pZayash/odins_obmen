Процедура ПакетыДанных_Сравнить(пПакетыДанных, пПараметрыВыполненияКоманды) Экспорт
	
	// Чтобы сравнить входящие данные, нужно как минимум два объекта
	Если пПакетыДанных.Количество() < 2 Тогда
		СообщПольз = Новый СообщениеПользователю;
		СообщПольз.Текст = "Для сравнения необходимо выбрать два объекта";
		СообщПольз.Сообщить();
		
		Возврат;
		
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	
	Заголовок = "Сравнение входящих данных: ";
	Для сч = 0 по 1 Цикл
		пакет = пПакетыДанных[сч];
		стрДанные = общ.ЗначениеРеквизитаОбъекта(пакет, "Данные", Истина);
		
		Порядок = сч + 1;
		ПараметрыОткрытия.Вставить("Текст" + Порядок, стрДанные);
		Заголовок = Заголовок + Порядок + "-" + Строка(пакет) 
		
	КонецЦикла;
	сч = Неопределено;
	
	ПараметрыОткрытия.Вставить("ЗаголовокФормы", Заголовок);
	
	ОткрытьФорму(
		"Обработка.общ_КонсольКода.Форма.общ", 
		ПараметрыОткрытия, 
		, // Форма владелец 
		Новый УникальныйИдентификатор, // УникальныйИдентификатор, 
		, // Окно
		, // Навигационная ссылка
		, // ОповещениеОЗакрытии,
		  // РежимОткрытияОкнаФормы.БлокироватьОкноВладельца// РежимОткрытияОкна
		);
	
КонецПроцедуры

// @TODO2 250717 Заяш: Дописать в определяемые команды
Процедура ПакетыДанных_ОбновитьПриемники(пПакетыДанных, ПараметрыВыполненияКоманды)
	
	обм_Команды.ПакетыДанных_ОбновитьПриемники(пПакетыДанных);
	
	// @TODO 250708 Заяш: Проверить все места исползования
	Оповестить("ВходящиеСообщения_ИзмененыСтатусы");

КонецПроцедуры

// @TODO2 250717 Заяш: Дописать в определяемые команды
Процедура Приемники_ОбновитьПоследнимПакетомДанных(пСсылкиПриемников, ПараметрыВыполненияКоманды)
	
	заполненныеПриемники = обм_Команды.Приемники_ОбновитьПоследнимПакетомДанных(пСсылкиПриемников);
	
	формаИсточник = Неопределено;
	Если
		ТипЗнч(ПараметрыВыполненияКоманды) = Тип("ПараметрыВыполненияКоманды")
		И	ТипЗнч(ПараметрыВыполненияКоманды.Источник) = Тип("ФормаКлиентскогоПриложения")
	Тогда
		формаИсточник = ПараметрыВыполненияКоманды.Источник; 
		
	КонецЕсли;

	Если заполненныеПриемники.Количество() = 1 Тогда
		Если формаИсточник = Неопределено Тогда
			ПоказатьЗначение(,заполненныеПриемники[0]);
			
		КонецЕсли;
		
		ОповеститьОбИзменении(заполненныеПриемники[0]);
		
	ИначеЕсли заполненныеПриемники.Количество() > 1 Тогда 
		ОповеститьОбИзменении(ТипЗнч(заполненныеПриемники[0]));
		
	ИначеЕсли заполненныеПриемники.Количество() = 0 Тогда
		Возврат;
		
	КонецЕсли; 

	// Обновить форму клиентского приложения, чтобы пользователь мог увидеть изменения
	Если формаИсточник <> Неопределено Тогда
		формаИсточник.Прочитать(); 
		формаИсточник.ОбновитьОтображениеДанных(); 

	КонецЕсли;
	
КонецПроцедуры

// @TODO2 250717 Заяш: Дописать в определяемые команды
Процедура Приемник_ЗагруженныеПакетыДанных(пПриемникСсылка, ПараметрыВыполненияКоманды)
	
	СтруктураОтбора = Новый Структура("Приемник", пПриемникСсылка);
	
	ОткрытьФорму("РегистрСведений.обм_Приемники_ЗагруженныеПакетыДанных.ФормаСписка", СтруктураОтбора, , пПриемникСсылка);

КонецПроцедуры
