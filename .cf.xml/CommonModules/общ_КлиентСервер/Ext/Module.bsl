Функция СКД_Группировка_Найти(пКоллекцияЭлементовСтруктурыНастроекКомпоновкиДанных, пИмяГруппировки) Экспорт

	Для Каждого элементСтруктуры Из пКоллекцияЭлементовСтруктурыНастроекКомпоновкиДанных Цикл
		Если ТипЗнч(элементСтруктуры) <> Тип("ГруппировкаКомпоновкиДанных") Тогда
			Продолжить;
			
		КонецЕсли;
		
		Если элементСтруктуры.Имя = пИмяГруппировки Тогда
			Возврат элементСтруктуры;
			
		КонецЕсли;
		
		подчиненнаяГруппировка = СКД_Группировка_Найти(элементСтруктуры.Структура, пИмяГруппировки);
		
		Если 
				ТипЗнч(подчиненнаяГруппировка) = Тип("ГруппировкаКомпоновкиДанных")
			И	подчиненнаяГруппировка.Имя = пИмяГруппировки
		Тогда
			Возврат подчиненнаяГруппировка;
			
		КонецЕсли;
		
	КонецЦикла;
	элементСтруктуры = Неопределено;
	
	Возврат Неопределено;
	
КонецФункции // СКД_Группировка_Найти 

Процедура СКД_ПользовательскаяНастройка_Установить(пКомпоновщикНастроек, пИдНастройки, пЗначение) Экспорт

	настройка = пКомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(пИдНастройки); 
	
	// Искать параметр по имени 
	Если настройка = Неопределено Тогда
		параметрСКД = 
			пКомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(
				Новый ПараметрКомпоновкиДанных(пИдНастройки)
				); 
		Если параметрСКД <> Неопределено Тогда
			настройка = 
				пКомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(
					параметрСКД.ИдентификаторПользовательскойНастройки
					);
					
		КонецЕсли;
				
	КонецЕсли; // настройка = Неопределено 

	// Искать по имени группировки
	Если настройка = Неопределено Тогда 
		группировка = СКД_Группировка_Найти(пКомпоновщикНастроек.Настройки.Структура, пИдНастройки);
		
		Если ТипЗнч(группировка) = Тип("ГруппировкаКомпоновкиДанных") Тогда
			настройка = группировка;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если настройка = Неопределено Тогда
		Возврат;
		
	КонецЕсли;
	
	Если ТипЗнч(настройка) = Тип("ГруппировкаКомпоновкиДанных") Тогда 
		Если ТипЗнч(пЗначение) = Тип("Булево") Тогда
			настройка.Использование = пЗначение;
			
		Иначе
			настройка.Использование = Истина; 
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(настройка) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
		настройка.Использование = Истина;
		настройка.Значение = пЗначение;
		
	КонецЕсли;

КонецПроцедуры  //  СКД_ПользовательскаяНастройка_Установить

