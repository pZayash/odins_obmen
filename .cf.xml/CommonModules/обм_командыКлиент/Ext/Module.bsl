////////////////////////////////////////////////////////////////////////////////
// <Заголовок модуля: краткое описание и условия применения модуля.>
//  
////////////////////////////////////////////////////////////////////////////////
#Область СлужебныйПрограммныйИнтерфейс

Процедура ПакетыДанных_Сравнить(пПакетыДанных, пПараметрыВыполненияКоманды) Экспорт
	
	// Чтобы сравнить входящие данные, нужно как минимум два объекта
	количествоСравниваемых = 2;
	Если пПакетыДанных.Количество() < количествоСравниваемых Тогда
		пользователю = Новый СообщениеПользователю;
		пользователю.Текст = "Для сравнения необходимо выбрать два объекта";
		пользователю.Сообщить();
		
		Возврат;
		
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	
	Заголовок = "Сравнение входящих данных: ";
	Для сч = 0 По 1 Цикл
		пакет = пПакетыДанных[сч];
		стрДанные = общ.ЗначениеРеквизитаОбъекта(пакет, "Данные", Истина);
		
		Порядок = сч + 1;
		ПараметрыОткрытия.Вставить("Текст" + Порядок, стрДанные);
		Заголовок = Заголовок + Порядок + "-" + Строка(пакет); 
		
	КонецЦикла;
	сч = Неопределено;
	
	ПараметрыОткрытия.Вставить("ЗаголовокФормы", Заголовок);

	формаВладелец = формаИсточник(пПараметрыВыполненияКоманды);
	ОткрытьФорму("Обработка.общ_КонсольКода.Форма.общ", 
		ПараметрыОткрытия, 
		формаВладелец, 
		пПараметрыВыполненияКоманды.Уникальность		);
	
КонецПроцедуры

Процедура ПакетыДанных_ОбновитьПриемники(пПакетыДанных) Экспорт
	
	обм_команды.ПакетыДанных_ОбновитьПриемники(пПакетыДанных);
	
КонецПроцедуры

Процедура Приемники_ОбновитьПоследнимПакетомДанных(пСсылкиПриемников, пПараметрыВыполненияКоманды) Экспорт
	
	заполненныеПриемники = обм_команды.Приемники_ОбновитьПоследнимПакетомДанных(пСсылкиПриемников);
	
	формаИсточник = формаИсточник(пПараметрыВыполненияКоманды);
	
	Если заполненныеПриемники.Количество() = 1 Тогда
		Если формаИсточник = Неопределено Тогда
			ПоказатьЗначение(, заполненныеПриемники[0]);
			
		КонецЕсли;
		
		ОповеститьОбИзменении(заполненныеПриемники[0]);
		
	ИначеЕсли заполненныеПриемники.Количество() > 1 Тогда 
		ОповеститьОбИзменении(ТипЗнч(заполненныеПриемники[0]));
		
	Иначе
		Возврат;
		
	КонецЕсли; 

	// Обновить форму клиентского приложения, чтобы пользователь мог увидеть изменения
	Если формаИсточник <> Неопределено Тогда
		формаИсточник.Прочитать(); 
		формаИсточник.ОбновитьОтображениеДанных(); 

	КонецЕсли;
	
КонецПроцедуры

Процедура Приемник_ЗагруженныеПакетыДанных(пСсылкиПриемников, пПараметрыВыполненияКоманды) Экспорт
	
	имяФормы = "РегистрСведений.обм_Приемники_ЗагруженныеПакетыДанных.ФормаСписка"; // BSLLS:Typo-off
	
	// Это команда для одного объекта
	приемникСсылка = общ.Значение_ПоМассиву(пСсылкиПриемников);
	значениеОтбора = Новый Структура("Приемник", приемникСсылка);
	параметрыФормы = Новый Структура("Отбор", значениеОтбора);
	формаВладелец = формаИсточник(пПараметрыВыполненияКоманды);
	ОткрытьФорму(имяФормы, параметрыФормы, формаВладелец);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция формаИсточник(пПараметрыВыполненияКоманды)
	
	результат = Неопределено;
	Если
		ТипЗнч(пПараметрыВыполненияКоманды) = Тип("ПараметрыВыполненияКоманды")
		И	ТипЗнч(пПараметрыВыполненияКоманды.Источник) = Тип("ФормаКлиентскогоПриложения")
	Тогда
		результат = пПараметрыВыполненияКоманды.Источник;
		
	КонецЕсли;
	
	Возврат результат;
	
КонецФункции

#КонецОбласти