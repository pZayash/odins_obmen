#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство(обм_лит.Ключ_ЗагрузитьИзФайлаПриОткрытии(), ЗагрузитьИзФайлаПриОткрытии);
	
	Если Не ЗначениеЗаполнено(Объект.Наименование) Тогда
		ЗагрузитьИзФайлаПриОткрытии = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗагрузитьИзФайлаПриОткрытии Тогда
		ЗагрузитьИзФайла();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда = Неопределено)

	фильтр = СтрШаблон("Внешние обработки (*.%1)|*.%1", "epf");
	параметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов("Выберите обработчик", Ложь, фильтр);
	
	оповещение = Новый ОписаниеОповещения("Загрузка_ПослеЗакрытияДиалогаВыбораФайла", ЭтаФорма);
	НачатьПомещениеФайлаНаСервер(оповещение,,,, параметрыДиалога, УникальныйИдентификатор);

КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Загрузка_ПослеЗакрытияДиалогаВыбораФайла(ОписаниеФайла, ДопПараметры) Экспорт
	
	Если ОписаниеФайла = Неопределено Или ОписаниеФайла.ПомещениеФайлаОтменено Тогда
		Возврат;
	КонецЕсли;
	
	ФайлНаСервереВХранилищеОбработчика(ОписаниеФайла.Адрес);
	
КонецПроцедуры

&НаСервере
Процедура ФайлНаСервереВХранилищеОбработчика(пАдресХранилища)

	Если Не ЭтоАдресВременногоХранилища(пАдресХранилища) Тогда 
		Возврат;
		
	КонецЕсли;
	
	справочникОбъект = РеквизитФормыВЗначение("Объект");
	
	двоичныеДанныеФайла = ПолучитьИзВременногоХранилища(пАдресХранилища);
	путьКФайлу = ПолучитьИмяВременногоФайла("epf");
	двоичныеДанныеФайла.Записать(путьКФайлу);

	текущийБезопасныйРежим = БезопасныйРежим();
	Если текущийБезопасныйРежим <> Ложь Тогда
		УстановитьОтключениеБезопасногоРежима(Истина);
				
	КонецЕсли;
			
	безЗащиты = Новый ОписаниеЗащитыОтОпасныхДействий;
	безЗащиты.ПредупреждатьОбОпасныхДействиях = Ложь;
		
	внешняяОбработка = ВнешниеОбработки.Создать(путьКФайлу, Ложь, безЗащиты);
	
	справочникОбъект.ВерсияИнтерфейса = внешняяОбработка.ВерсияИнтерфейса();
	справочникОбъект.Комментарий = внешняяОбработка.Метаданные().Комментарий;
	справочникОбъект.Наименование = внешняяОбработка.Метаданные().Имя;
	
	справочникОбъект.Синоним = внешняяОбработка.Метаданные().Синоним;
	справочникОбъект.ХранилищеОбработчика = Новый ХранилищеЗначения(двоичныеДанныеФайла);
	
	справочникОбъект.Записать();
	
	ЗначениеВРеквизитФормы(справочникОбъект, "Объект");

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПоместитьФайлОбработчикаВоВременноеХранилище(пОбработчикСсылка)
	хранилищеОбработчика = общ.ЗначениеРеквизитаОбъекта(пОбработчикСсылка, "ХранилищеОбработчика");
	Возврат ПоместитьВоВременноеХранилище(хранилищеОбработчика.Получить());
	
КонецФункции

&НаКлиенте
Процедура ВыгрузитьВФайл(Команда)
	
	адресВХ = ПоместитьФайлОбработчикаВоВременноеХранилище(Объект.Ссылка);
	параметрыДиалога = 
		Новый ПараметрыДиалогаПолученияФайлов(
			"Выберите место сохранения файла обработчика",
			Ложь // Выбор каталога
			);
			
	НачатьПолучениеФайлаССервера(адресВХ, Объект.Наименование + ".epf", параметрыДиалога);

КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
