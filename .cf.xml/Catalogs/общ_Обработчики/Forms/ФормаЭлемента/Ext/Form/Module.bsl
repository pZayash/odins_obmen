#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство(обм_лит.Ключ_ЗагрузитьИзФайлаПриОткрытии(), ЗагрузитьИзФайлаПриОткрытии);
	
	Если Не ЗначениеЗаполнено(Объект.Наименование) Тогда
		ЗагрузитьИзФайлаПриОткрытии = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗагрузитьИзФайлаПриОткрытии Тогда
		ЗагрузитьИзФайла();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда = Неопределено)

	фильтр = СтрШаблон("Внешние обработки (*.%1)|*.%1", "epf");
	параметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов("Выберите обработчик", Ложь, фильтр);
	
	действие = л_действие_ЗагрузитьИзФайла();
	Если Команда <> Неопределено И ЗначениеЗаполнено(Команда.Действие) Тогда
		действие = Команда.Действие;
		
	КонецЕсли;
	
	допПараметры = Новый Структура("Действие", действие);
	оповещение = Новый ОписаниеОповещения("Загрузка_ПослеЗакрытияДиалогаВыбораФайла", ЭтаФорма, допПараметры);
	НачатьПомещениеФайлаНаСервер(оповещение,,,, параметрыДиалога, УникальныйИдентификатор);

КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыгрузитьВФайл(Команда)
	
	адресВХ = ПоместитьФайлОбработчикаВоВременноеХранилище(Объект.Ссылка);
	параметрыДиалога = 
		Новый ПараметрыДиалогаПолученияФайлов(
			"Выберите место сохранения файла обработчика",
			Ложь // Выбор каталога
			);
			
	НачатьПолучениеФайлаССервера(адресВХ, Объект.Наименование + ".epf", параметрыДиалога);

КонецПроцедуры

&НаКлиенте
Процедура Загрузка_ПослеЗакрытияДиалогаВыбораФайла(ОписаниеФайла, ДопПараметры) Экспорт
	
	Если ОписаниеФайла = Неопределено Или ОписаниеФайла.ПомещениеФайлаОтменено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДопПараметры = Неопределено Тогда
		Возврат;
		
	КонецЕсли;
	
	Если ДопПараметры.Действие = л_действие_ЗагрузитьИзФайла() Тогда
		ФайлНаСервереВХранилищеОбработчика(ОписаниеФайла.Адрес);
		
	ИначеЕсли ДопПараметры.Действие = л_действие_ПутьКФайлуДляОтладки() Тогда
		Объект.ПутьКФайлуДляОтладки = ОписаниеФайла.СсылкаНаФайл.Файл.ПолноеИмя;
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция л_действие_ЗагрузитьИзФайла()
	
	Возврат "ЗагрузитьИзФайла";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция л_действие_ПутьКФайлуДляОтладки()
	
	Возврат "ПутьКФайлуДляОтладки";
	
КонецФункции

&НаСервереБезКонтекста
Функция ПоместитьФайлОбработчикаВоВременноеХранилище(пОбработчикСсылка)
	хранилищеОбработчика = общ.ЗначениеРеквизитаОбъекта(пОбработчикСсылка, "ХранилищеОбработчика");
	Возврат ПоместитьВоВременноеХранилище(хранилищеОбработчика.Получить());
	
КонецФункции

&НаКлиенте
Процедура ПутьКФайлуДляОтладкиНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ЗагрузитьИзФайла(Новый Структура("Действие", л_действие_ПутьКФайлуДляОтладки())); 
	
КонецПроцедуры

&НаСервере
Процедура ФайлНаСервереВХранилищеОбработчика(пАдресХранилища)

	Если Не ЭтоАдресВременногоХранилища(пАдресХранилища) Тогда 
		Возврат;
		
	КонецЕсли;
	
	справочникОбъект = РеквизитФормыВЗначение("Объект");
	
	двоичныеДанныеФайла = ПолучитьИзВременногоХранилища(пАдресХранилища);
	путьКФайлу = ПолучитьИмяВременногоФайла("epf");
	двоичныеДанныеФайла.Записать(путьКФайлу);

	текущийБезопасныйРежим = БезопасныйРежим();
	Если текущийБезопасныйРежим <> Ложь Тогда
		УстановитьОтключениеБезопасногоРежима(Истина);
				
	КонецЕсли;
			
	безЗащиты = Новый ОписаниеЗащитыОтОпасныхДействий;
	безЗащиты.ПредупреждатьОбОпасныхДействиях = Ложь;
		
	внешняяОбработка = ВнешниеОбработки.Создать(путьКФайлу, Ложь, безЗащиты);
	
	справочникОбъект.ВерсияИнтерфейса = внешняяОбработка.ВерсияИнтерфейса();
	справочникОбъект.Комментарий = внешняяОбработка.Метаданные().Комментарий;
	справочникОбъект.Наименование = внешняяОбработка.Метаданные().Имя;
	
	справочникОбъект.Синоним = внешняяОбработка.Метаданные().Синоним;
	справочникОбъект.ХранилищеОбработчика = Новый ХранилищеЗначения(двоичныеДанныеФайла);
	
	справочникОбъект.Записать();
	
	ЗначениеВРеквизитФормы(справочникОбъект, "Объект");

КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
