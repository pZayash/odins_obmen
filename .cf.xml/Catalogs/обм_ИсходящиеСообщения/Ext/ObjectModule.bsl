#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет необходимые для работы алгоритмов обмена реквизиты
// Не перезаписывает, только заполняет пустые
//
Процедура Конструктор(пИсточник = Неопределено, пТекстСообщения = Неопределено, 
	пУзлыПолучатели = Неопределено, пОбработчикОтправки = Неопределено) Экспорт
	
	исходящее = ЭтотОбъект;
	
	общ.ЕслиПустое(исходящее.Источник, пИсточник);
	общ.ЕслиПустое(исходящее.ТекстСообщения, пТекстСообщения);
	
	Если Получатели.Количество() = 0 Тогда
		узлы = СтрРазделить(пУзлыПолучатели, ",", Ложь);
		Для Каждого узел Из узлы Цикл
			рядПолучатель = исходящее.Получатели.Добавить();
			рядПолучатель.Узел = обм.УзелПривести(узел);
			
		КонецЦикла;
		узел = Неопределено;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(исходящее.ОбработчикОтправки) Тогда
		обработчик = Справочники.общ_Обработчики.НайтиСсылку(пОбработчикОтправки);
		исходящее.ОбработчикОтправки = обработчик;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ЭтоНовый() Тогда
		ЭтотОбъект.ДатаВремя = общ.ДатаУниверсальная();
		
	КонецЕсли;
	
	л_узел = "Узел";
	Получатели.Свернуть(л_узел);
	Получатели.Сортировать(л_узел);
	индекс = Получатели.Количество();
	Пока индекс > 0 Цикл
		индекс = индекс - 1;
		рядПолучатели = Получатели[индекс];
		узел = обм.УзелПривести(рядПолучатели.Узел);
		Если Не ПустаяСтрока(узел) Тогда
			рядПолучатели.Узел = узел;
			
		Иначе
			Получатели.Удалить(индекс);
			
		КонецЕсли;
		
	КонецЦикла;
	индекс = Неопределено;
	
	всеПолучатели = Получатели.ВыгрузитьКолонку(л_узел);
	ПолучателиСтрокой = СтрСоединить(всеПолучатели, ",");
	
	Если ПустаяСтрока(Наименование) Тогда
		Наименование = Строка(Источник) + " >>>> " + ПолучателиСтрокой;
		
	КонецЕсли;
	
	УзелОтправительИсточника = обм.УзелПривести(УзелОтправительИсточника);
	
КонецПроцедуры

#КонецОбласти // Обработчики событий

#Иначе // Если Сервер и тп
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");

#КонецЕсли // Если Сервер и тп