&НаКлиенте
Процедура ОбработкаКоманды(МассивСсылкаПриемник, ПараметрыВыполненияКоманды)
	
	заполненныеПриемники = ОбработкаКомандыСервер(МассивСсылкаПриемник);
	
	формаИсточник = Неопределено;
	Если  
			ТипЗнч(ПараметрыВыполненияКоманды) = Тип("ПараметрыВыполненияКоманды")
		И	ТипЗнч(ПараметрыВыполненияКоманды.Источник) = Тип("ФормаКлиентскогоПриложения")
	Тогда
		формаИсточник = ПараметрыВыполненияКоманды.Источник; 
		
	КонецЕсли;

	
	Если заполненныеПриемники.Количество() = 1 Тогда
		Если формаИсточник = Неопределено Тогда
			ПоказатьЗначение(,заполненныеПриемники[0]);
			
		КонецЕсли;
		
		ОповеститьОбИзменении(заполненныеПриемники[0]);
		
	ИначеЕсли заполненныеПриемники.Количество() > 1 Тогда 
		ОповеститьОбИзменении(ТипЗнч(заполненныеПриемники[0]));
		
	ИначеЕсли заполненныеПриемники.Количество() = 0 Тогда
		Возврат;
		
	КонецЕсли; 

	// Обновить форму клиентского приложения, чтобы пользователь мог увидеть изменения
	Если формаИсточник <> Неопределено Тогда
		формаИсточник.Прочитать(); 
		формаИсточник.ОбновитьОтображениеДанных(); 

	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОбработкаКомандыСервер(пСсылкиПриемников)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Приемники", пСсылкиПриемников);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПоследнееЗаполнение.Приемник КАК Приемник,
	|	ПоследнееЗаполнение.ПакетДанных КАК ПакетДанных,
	|	ПоследнееЗаполнение.ОбработчикПакетаДанных КАК ОбработчикПакетаДанных
	|ИЗ
	|	РегистрСведений.обм_СвязиПриемникПакетДанных.СрезПоследних(, Приемник В (&Приемники)) КАК ПоследнееЗаполнение";
	
	выборка = Запрос.Выполнить().Выбрать();
	
	заполненныеПриемники = Новый Массив;
	
	загрузчик = Обработки.обм_ПакетыДанных_Обработка.Создать();
	Пока выборка.Следующий() Цикл
		приемникОбъект = загрузчик.Приемник_Заполнить(выборка.Приемник, выборка.ПакетДанных, выборка.ОбработчикПакетаДанных);
		
		объектСсылкаДругое = общ.ОбъектСсылкаДругое(приемникОбъект);
		
		Если объектСсылкаДругое = "Объект" Тогда
			заполненныеПриемники.Добавить(приемникОбъект.Ссылка); 
			
		ИначеЕсли объектСсылкаДругое = "Ссылка" Тогда
			заполненныеПриемники.Добавить(приемникОбъект);
			
		КонецЕсли;
		
	КонецЦикла;
	выборка.Сбросить();

	Возврат заполненныеПриемники;
	
КонецФункции