// Функция поиска приемника по умолчанию
Функция НайтиПриемника(пДанныеЗаполненияСсылка) Экспорт
	
	общ.Проверка_Тип(пДанныеЗаполненияСсылка, Тип("СправочникСсылка.обм_ДанныеЗаполнения"));
	
	// 1. Искать ссылку на входящий объект в регистре сведений
	СсылкаПриемник = 
		РегистрыСведений.обм_Приемники.НайтиСсылкуПриемника(
			пДанныеЗаполненияСсылка.Узел,
			пДанныеЗаполненияСсылка.Категория,
			пДанныеЗаполненияСсылка.Идентификатор
			);
			
	Если ЗначениеЗаполнено(СсылкаПриемник) Тогда
		Возврат СсылкаПриемник;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция ПолучитьОбъект(пВходящееСообщениеВладелец, пУзел, пКатегория, пИдентификатор) Экспорт
	
	общ.Проверка_Тип(пВходящееСообщениеВладелец, Тип("СправочникСсылка.обм_ВходящиеСообщения"));
	
	Если ПустаяСтрока(пУзел) Тогда
		ВызватьИсключение "Не заполнен параметр 'узел'";
		
	КонецЕсли;
	
	Если ПустаяСтрока(пУзел) Тогда
		ВызватьИсключение "Не заполнен параметр 'категория'";
		
	КонецЕсли;
	
	Если ПустаяСтрока(пИдентификатор) Тогда
		ВызватьИсключение "Не заполнен параметр 'идентификатор'";
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("пВходящееСообщениеВладелец", пВходящееСообщениеВладелец);
	Запрос.УстановитьПараметр("Узел", пУзел);
	Запрос.УстановитьПараметр("Категория", пКатегория);
	Запрос.УстановитьПараметр("Идентификатор", пИдентификатор);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДЗ.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.обм_ДанныеЗаполнения КАК ДЗ
	|ГДЕ
	|	ДЗ.Владелец = &пВходящееСообщениеВладелец
	|	И ДЗ.Узел = &Узел
	|	И ДЗ.Категория = &Категория
	|	И ДЗ.Идентификатор = &Идентификатор
	|	И ДЗ.Родитель = ЗНАЧЕНИЕ(Справочник.обм_ДанныеЗаполнения.ПустаяСсылка)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ДанныеЗаполненияОбъект = Неопределено;
	Если Выборка.Следующий() Тогда
		ДанныеЗаполненияОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
	Иначе
		ДанныеЗаполненияОбъект = Справочники.обм_ДанныеЗаполнения.СоздатьЭлемент();
		
	КонецЕсли; 
	
	ДанныеЗаполненияОбъект.Владелец = пВходящееСообщениеВладелец;
	ДанныеЗаполненияОбъект.ДатаВремя = пВходящееСообщениеВладелец.ДатаВремя;
	ДанныеЗаполненияОбъект.ПометкаУдаления = Ложь;
	ДанныеЗаполненияОбъект.Узел = пУзел;
	ДанныеЗаполненияОбъект.Категория = пКатегория;
	ДанныеЗаполненияОбъект.Идентификатор = пИдентификатор;
	ДанныеЗаполненияОбъект.Данные = Неопределено; 
	
	Возврат ДанныеЗаполненияОбъект;
	
КонецФункции

