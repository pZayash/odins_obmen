
Процедура ПередЗаписью(Отказ) 
	
	// Прочитать из потомков реквизиты для отложенной записи
	// Для новых и для потомков эта обработка не имеет смыслк
	Если Не ЭтотОбъект.ЭтоНовый() И Не ЗначениеЗаполнено(ЭтотОбъект.Родитель) Тогда
		зпрс = Новый Запрос;
		зпрс.УстановитьПараметр("Родитель", ЭтотОбъект.Ссылка);
		зпрс.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Т.Ссылка КАК Ссылка,
		|	Т.Обработчик КАК Обработчик,
		|	Т.Приемник_JSON КАК Приемник_JSON
		|ИЗ
		|	Справочник.обм_ДанныеЗаполнения КАК Т
		|ГДЕ
		|	Т.Родитель = &Родитель
		|
		|УПОРЯДОЧИТЬ ПО
		|	Т.ДатаВремя УБЫВ"; 
		
		выборка = зпрс.Выполнить().Выбрать();
		этоПервый = Истина;
		
		Пока выборка.Следующий() Цикл 
			Если этоПервый Тогда  
				ЭтотОбъект.Обработчик = выборка.Обработчик;
				ЭтотОбъект.Приемник_JSON = выборка.Приемник_JSON;
				
			КонецЕсли;
			этоПервый = Ложь;

			потомокОбъект = Выборка.Ссылка.ПолучитьОбъект();
			потомокОбъект.ОбменДанными.Загрузка = Истина;
			потомокОбъект.Удалить();
			
		КонецЦикла;
		
	КонецЕсли;

	общ.СправочникОбъект_УстановитьУИДкакКод(ЭтотОбъект);

	ЭтотОбъект.Наименование =
		СтрШаблон(
			"%1 %2 Идентификатор: %3 owner: %4",
			Узел, // %1
			Категория, // %2
			Идентификатор, // %3
			Строка(ЭтотОбъект.Владелец.УникальныйИдентификатор()) // %4
			);

		Данные_Длина = СтрДлина(Данные);

	Если Не ПустаяСтрока(Данные) Тогда
		Хеширование = Новый ХешированиеДанных(ХешФункция.sha256);
		Хеширование.Добавить(Данные);
		Данные_sha256 = Строка(Хеширование.ХешСумма);
		
	Иначе
		Данные = Неопределено;
		Данные_sha256 = Неопределено;
		
	КонецЕсли;  

	Если Не ПустаяСтрока(Приемник_JSON) Тогда
		Хеширование = Новый ХешированиеДанных(ХешФункция.sha256);
		Хеширование.Добавить(Приемник_JSON);
		Приемник_sha256 = Строка(Хеширование.ХешСумма);
		
	Иначе
		Приемник_JSON = Неопределено;
		Приемник_sha256 = Неопределено;
		
	КонецЕсли; 
	
	Если ПустаяСтрока(Данные_sha256) Тогда
		Данные_sha256 = Неопределено;
		
	КонецЕсли;
	
	Если ПустаяСтрока(Приемник_sha256) Тогда
		Приемник_sha256 = Неопределено;
		
	КонецЕсли;  
	
	Если Не ЗначениеЗаполнено(ЭтотОбъект.ДатаВремя) Тогда
		ЭтотОбъект.ДатаВремя = общ_Сервер.ТекущаяДатаНаСервере();
		
	КонецЕсли;
		
КонецПроцедуры
