
&НаКлиенте
Процедура ОбработкаКоманды(пДанныеВходящихСообщенийДляОбработки, ПараметрыВыполненияКоманды)
	
	МассивСсылокЗаполненныхПриемников = ОбработкаКомандыСервер(пДанныеВходящихСообщенийДляОбработки);
	
	Если МассивСсылокЗаполненныхПриемников.Количество() = 1 Тогда
		ПоказатьЗначение(,МассивСсылокЗаполненныхПриемников[0]);
		ОповеститьОбИзменении(МассивСсылокЗаполненныхПриемников[0]);
		
	Иначе
		соотвТипы = Новый Соответствие;
		Для Каждого СсылкаПриемник Из МассивСсылокЗаполненныхПриемников Цикл
			соотвТипы.Вставить(ТипЗнч(СсылкаПриемник));
			
		КонецЦикла;
		СсылкаПриемник = Неопределено;
		
		Для Каждого Пара Из соотвТипы Цикл
			ОповеститьОбИзменении(Пара.Ключ);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОбработкаКомандыСервер(пДанныеВходящихСообщенийДляОбработки)
	
	МассивСсылокЗаполненныхПриемников = Новый Массив;

	Обработчик = Обработки.обм_ВходящиеСообщения_Обработка.Создать();
	
	данныеЗаполненияМассив = 
		Обработчик.ДанныеЗаполнения_Сортировать(пДанныеВходящихСообщенийДляОбработки);
			
	Для Каждого данныеЗаполненияСсылка Из данныеЗаполненияМассив Цикл
		
		// Искать ссылку приемника
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("данныеЗаполненияСсылка", данныеЗаполненияСсылка);
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	КлючПриемника.Узел КАК Узел,
		|	КлючПриемника.Категория КАК Категория,
		|	КлючПриемника.Идентификатор КАК Идентификатор
		|ПОМЕСТИТЬ КлючПриемника
		|ИЗ
		|	Справочник.обм_ДанныеЗаполнения КАК КлючПриемника
		|ГДЕ
		|	КлючПриемника.Ссылка = &данныеЗаполненияСсылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Приемники.Приемник КАК Приемник
		|ИЗ
		|	КлючПриемника КАК КлючПриемника
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.обм_Приемники КАК Приемники
		|		ПО КлючПриемника.Узел = Приемники.Узел
		|			И КлючПриемника.Категория = Приемники.Категория
		|			И КлючПриемника.Идентификатор = Приемники.Идентификатор";
		
		Выборка = Запрос.Выполнить().Выбрать();

		Если Выборка.Следующий() И ЗначениеЗаполнено(Выборка.Приемник) Тогда
			Приемник = Обработчик.Приемник_Заполнить(Выборка.Приемник, данныеЗаполненияСсылка);
			Обработчик.Приемник_ПослеЗаписи(Приемник, данныеЗаполненияСсылка);
			
			Если общ.ОбъектСсылкаДругое(Приемник) = "Объект" Тогда
				МассивСсылокЗаполненныхПриемников.Добавить(Приемник.Ссылка); 
				
			Иначе
				МассивСсылокЗаполненныхПриемников.Добавить(Приемник);
				
			КонецЕсли;
			
		Иначе
			сообщПользователю = Новый СообщениеПользователю;
			сообщПользователю.Текст = 
				СтрШаблон("Не найден приемник для %1", данныеЗаполненияСсылка);
			сообщПользователю.Сообщить();
			
		КонецЕсли;

	КонецЦикла;
	данныеЗаполненияСсылка = Неопределено;
	
	Возврат МассивСсылокЗаполненныхПриемников;

КонецФункции
