&НаКлиенте
Процедура ОбработкаКоманды(СсылкаПриемник, ПараметрыВыполненияКоманды)
	
	СтруктураОтбора = ОбработкаКомандыСервер(СсылкаПриемник);
	
	Если СтруктураОтбора = Неопределено Тогда
		Сообщить("Нет входящих данных");
		Возврат;
		
	КонецЕсли;
	
	ОткрытьФорму(
		"Справочник.обм_ДанныеЗаполнения.ФормаСписка", 
		СтруктураОтбора,
		, // Владелец
		СсылкаПриемник
		);

КонецПроцедуры

&НаСервере
Функция ОбработкаКомандыСервер(СсылкаПриемник)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Приемник", СсылкаПриемник);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Т.ДанныеЗаполнения.Узел КАК Узел,
	|	Т.ДанныеЗаполнения.Идентификатор КАК Идентификатор,
	|	Т.ДанныеЗаполнения.Категория КАК Категория
	|ИЗ
	|	РегистрСведений.обм_Приемники_ДанныеЗаполнения.СрезПоследних(, Приемник = &Приемник) КАК Т";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Результат = Новый Структура("Узел, Категория, Идентификатор");
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
		Возврат Результат;
		
	Иначе
		Возврат Неопределено;
		
	КонецЕсли;
	
КонецФункции