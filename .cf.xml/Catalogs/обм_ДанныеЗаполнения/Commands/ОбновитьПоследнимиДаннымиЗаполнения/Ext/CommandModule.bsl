&НаКлиенте
Процедура ОбработкаКоманды(МассивСсылкаПриемник, ПараметрыВыполненияКоманды)
	
	МассивСсылокЗаполненныхПриемников = ОбработкаКомандыСервер(МассивСсылкаПриемник);
	
	формаИсточник = Неопределено;
	Если  
			ТипЗнч(ПараметрыВыполненияКоманды) = Тип("ПараметрыВыполненияКоманды")
		И	ТипЗнч(ПараметрыВыполненияКоманды.Источник) = Тип("ФормаКлиентскогоПриложения")
	Тогда
		формаИсточник = ПараметрыВыполненияКоманды.Источник; 
		
	КонецЕсли;

	
	Если МассивСсылокЗаполненныхПриемников.Количество() = 1 Тогда
		Если формаИсточник = Неопределено Тогда
			ПоказатьЗначение(,МассивСсылокЗаполненныхПриемников[0]);
			
		КонецЕсли;
		
		ОповеститьОбИзменении(МассивСсылокЗаполненныхПриемников[0]);
		
	ИначеЕсли МассивСсылокЗаполненныхПриемников.Количество() > 1 Тогда 
		ОповеститьОбИзменении(ТипЗнч(МассивСсылокЗаполненныхПриемников[0]));
		
	ИначеЕсли МассивСсылокЗаполненныхПриемников.Количество() = 0 Тогда
		Возврат;
		
	КонецЕсли; 

	// Обновить форму клиентского приложения, чтобы пользователь мог увидеть изменения
	Если формаИсточник <> Неопределено Тогда
		Попытка
			формаИсточник.Прочитать(); 
		Исключение
		КонецПопытки;
		Попытка
			формаИсточник.ОбновитьОтображениеДанных(); 
		Исключение
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОбработкаКомандыСервер(массивПриемникСсылка)
	
	МассивСсылокЗаполненныхПриемников = Новый Массив;
	Обработчик = Обработки.обм_ВходящиеСообщения_Обработка.Создать();
	
	Для Каждого приемникСсылка Из массивПриемникСсылка Цикл
	
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Приемник", приемникСсылка);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Т.ДанныеЗаполнения.Узел КАК Узел,
		|	Т.ДанныеЗаполнения.Категория КАК Категория,
		|	Т.ДанныеЗаполнения.Идентификатор КАК Идентификатор
		|ПОМЕСТИТЬ РеквизитыПриемника
		|ИЗ
		|	РегистрСведений.обм_Приемники_ДанныеЗаполнения.СрезПоследних(, Приемник = &Приемник) КАК Т
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ДанныеЗаполнения.Ссылка КАК ОбъектВходящегоЗапросаСсылка
		|ИЗ
		|	РеквизитыПриемника КАК РеквизитыПриемника
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.обм_ДанныеЗаполнения КАК ДанныеЗаполнения
		|		ПО РеквизитыПриемника.Узел = ДанныеЗаполнения.Узел
		|			И РеквизитыПриемника.Категория = ДанныеЗаполнения.Категория
		|			И РеквизитыПриемника.Идентификатор = ДанныеЗаполнения.Идентификатор
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДанныеЗаполнения.ДатаВремя УБЫВ";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() И ЗначениеЗаполнено(Выборка.ОбъектВходящегоЗапросаСсылка) Тогда
			Приемник = Обработчик.Приемник_Заполнить(приемникСсылка, Выборка.ОбъектВходящегоЗапросаСсылка);
			Обработчик.Приемник_ПослеЗаписи(Приемник, Выборка.ОбъектВходящегоЗапросаСсылка);
			
			Если общ.ОбъектСсылкаДругое(Приемник) = "Объект" Тогда
				МассивСсылокЗаполненныхПриемников.Добавить(Приемник.Ссылка); 
				
			Иначе
				МассивСсылокЗаполненныхПриемников.Добавить(Приемник);
				
			КонецЕсли;
			
		КонецЕсли;

	КонецЦикла;
	приемникСсылка = Неопределено;
	
	Возврат МассивСсылокЗаполненныхПриемников;
	
КонецФункции