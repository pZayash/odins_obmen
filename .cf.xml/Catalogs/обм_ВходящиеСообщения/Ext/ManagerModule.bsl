#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ДанныеЗаполнения_Обработать(пВходящиеСообщения) Экспорт
	
	входящиеСообщения = ВходящиеСообщения_ПроверитьСортировать(пВходящиеСообщения);
	
	Для Каждого входящее Из входящиеСообщения Цикл
		
		Отказ = Ложь;
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("входящиеСообщения", пВходящиеСообщения);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Т.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.обм_ПакетыДанных КАК Т
		|ГДЕ
		|	Т.ПометкаУдаления = ЛОЖЬ
		|	И Т.Владелец В(&входящиеСообщения)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Т.ДатаВремя УБЫВ";
		
		данныеЗаполнения = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
		
		Если данныеЗаполнения.Количество() = 0 Тогда
			// ОбработанСОшибкой, нет объектов для обработки
			примечание = "Нет данных заполнения";
			Справочники.обм_ВходящиеСообщения.Статус_Установить( // ОбработанСОшибкой  
				входящее, 
				Перечисления.обм_ВходящиеСообщения_Статусы.ОбработанСОшибкой,
				примечание 
				);
			
		Иначе
			обработчикДанныхЗаполнения = Обработки.обм_ПакетыДанных_Обработка.Создать();
			обработчикДанныхЗаполнения.ДанныеЗаполнения_Обработать(данныеЗаполнения);
			
			статус = 
				?(обработчикДанныхЗаполнения.ЕстьОшибки(),
					Перечисления.обм_ВходящиеСообщения_Статусы.ОбработанСОшибкой,
					Перечисления.обм_ВходящиеСообщения_Статусы.Обработан
					);
					
			примечание = обработчикДанныхЗаполнения.ПримечаниеОЗагрузке();
			
			Справочники.обм_ВходящиеСообщения.Статус_Установить(входящее, статус, примечание);

		КонецЕсли;
			
	КонецЦикла;
	входящее = Неопределено;
	
КонецПроцедуры

Процедура ПакетыДанных_Извлечь(пВходящиеСообщения) Экспорт
	
	входящиеСообщения = ВходящиеСообщения_ПроверитьСортировать(пВходящиеСообщения);
	
	// Обработка пустого массива не имеет смысла
	Если входящиеСообщения.Количество() = 0 Тогда
		Возврат;
		
	КонецЕсли;
	
	// Основной цикл
	извлекатель = Обработки.обм_ВходящиеСообщение_ИзвлечениеПакетовДанных_ПереопределяемыйИнтерфейс.Создать();
	реализацииИнтерфейса = общ.Интерфейс_НайтиРеализации(извлекатель);
	
	естьОбработчики = реализацииИнтерфейса.Количество() > 0;
	
	Для Каждого входящее Из входящиеСообщения Цикл
		
		// Проверка 
		Если Не общ.ЭтоЗаполненнаяСсылка(входящее, Тип("СправочникСсылка.обм_ВходящиеСообщения")) Тогда 
			Продолжить;

		КонецЕсли;
		
		// Проверить что входящее сообщение находиться в допустимом статус
		Если СозданиеДанныхЗаполненияРазрешено(входящее) Тогда 
			Продолжить;
					
		КонецЕсли;
		
		// Существующие подчиненные объекты входящих запросов пометить на удаление
		// чтобы в процессе обработки не были созданы дубли
		ПометитьНаУдалениеПодчиненныеДанныеЗаполнения(входящее);
		
		примечание = "";
		обработкаВыполнена = Ложь;
		счетчикОшибок = 0;

		Если естьОбработчики Тогда
			Для Каждого обработчик Из реализацииИнтерфейса Цикл
				общ.Интерфейс_УстановитьРеализацию(извлекатель, обработчик);
					
				примечаниеОбработчика = "";
					
				// Нужно ли обрабатывать это сообщение
				обрабатывается = извлекатель.ВходящееСообщение_Обрабатывается(входящее); // Проверка содержимого запроса
				Если Не обрабатывается Тогда
					Продолжить;
					
				КонецЕсли;
	
				// Создание данных заполнения делать в попытке, чтобы падение переопределяемого обработчика не привело к тому,
				// что не запишуться все данные заполнения, а не только упавшие с ошибкой
				Попытка
					// Разобрать входящее сообщение на данные заполнения
					пакетыДанныхОбъекты = извлекатель.ПакетыДанных_Извлечь(входящее, примечаниеОбработчика);
					обработкаВыполнена = Истина;

				Исключение
					счетчикОшибок = счетчикОшибок + 1;
					инфОбОшибке = ИнформацияОбОшибке();
					Ошибка_ВЖурнал(
						входящее, 
						инфОбОшибке, 
						обработчик,
						, // Пакет данных
						"ПакетДанных.Создать"
						);

					ДополнитьПримечание(примечание, "Ошибка создания данных заполнения, см. журнал регистрации");
					обОшибкеКратко = общ.Ошибка_КраткоеПредставление(инфОбОшибке);
					ДополнитьПримечание(примечание, обОшибкеКратко);
						
				КонецПопытки;

				ДополнитьПримечание(примечание, примечаниеОбработчика);
				
				Для Каждого пакетДанныхОбъект Из пакетыДанныхОбъекты Цикл
					Если ТипЗнч(пакетДанныхОбъект) <> Тип("СправочникОбъект.обм_ПакетыДанных") Тогда
						счетчикОшибок = счетчикОшибок + 1;
						комментарийОшибки = 
							СтрШаблон(
								"Обработчик %1 вернул неправильный результат. Ожидался СправочникОбъект.обм_ПакетыДанных, получен %2 типа %3",
								Справочники.общ_Обработчики.НайтиСсылку(обработчик), // %1
								Строка(пакетДанныхОбъект), // %2
								общ.Тип_Представление(пакетДанныхОбъект) // %3;
								);
								
						Ошибка_ВЖурнал(
							входящее, 
							комментарийОшибки, 
							обработчик,
							, // ПакетДанных
							"ПакетДанных.Создать"
							);
							
						ДополнитьПримечание(примечание, комментарийОшибки);
						
						Продолжить;
						
					КонецЕсли;
					
					Попытка
						пакетДанныхОбъект.Записать();
						
					Исключение
						счетчикОшибок = счетчикОшибок + 1;
						инфОбОшибке = ИнформацияОбОшибке();
						Ошибка_ВЖурнал(
							входящее, 
							инфОбОшибке, 
							обработчик,
							пакетДанныхОбъект,
							"ПакетДанных.Записать"
							);
						
						ДополнитьПримечание(примечание, "Ошибка записи данных заполнения, см. журнал регистрации");
						обОшибкеКратко = общ.Ошибка_КраткоеПредставление(инфОбОшибке);
						ДополнитьПримечание(примечание, обОшибкеКратко);
							
					КонецПопытки;
					
				КонецЦикла;
				пакетДанныхОбъект = Неопределено;
						
			КонецЦикла;
			обработчик = Неопределено;
				
		Иначе // Нет обработчиков
			примечание = обм_лит.НетОбработчика();
			обработкаВыполнена = Ложь;
				
		КонецЕсли; // Если естьОбработчики

		// Установка статуса
		статус = 
			?( обработкаВыполнена,
				Перечисления.обм_ВходящиеСообщения_Статусы.ЖдетОбработки,
				Перечисления.обм_ВходящиеСообщения_Статусы.НеОбрабатывается
				);
				
		Справочники.обм_ВходящиеСообщения.Статус_Установить(входящее, статус, примечание, счетчикОшибок);

	КонецЦикла;
	входящее = Неопределено;
	
КонецПроцедуры 

Функция Статус(пВходящееСообщениеСсылка) Экспорт 

	Если 
		Не Общ.ЭтоЗаполненнаяСсылка(пВходящееСообщениеСсылка, Тип("СправочникСсылка.обм_ВходящиеСообщения"))
	Тогда
		Возврат Неопределено;
		
	КонецЕсли;

	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("пВходящееСообщениеСсылка", пВходящееСообщениеСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Т.Статус КАК Статус
	|ИЗ
	|	РегистрСведений.обм_ВходящиеСообщения_Статусы.СрезПоследних(, ВходящееСообщение = &пВходящееСообщениеСсылка) КАК Т";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Статус;
		
	Иначе
		Возврат Неопределено;
		
	КонецЕсли;
	
КонецФункции

Процедура Статус_Установить(пВходящееСообщениеСсылка, пСтатус, пПримечание = Неопределено, пКоличествоОшибок = 0) Экспорт
	
	Если 
			Не Общ.ЭтоЗаполненнаяСсылка(пВходящееСообщениеСсылка, Тип("СправочникСсылка.обм_ВходящиеСообщения"))
		Или	Не Общ.ЭтоЗаполненнаяСсылка(пСтатус, Тип("ПеречислениеСсылка.обм_ВходящиеСообщения_Статусы")) 
	Тогда
		Возврат;
		
	КонецЕсли;

	стрПримечание = Неопределено;
	
	Если ТипЗнч(пПримечание) = Тип("Строка") Тогда
		стрПримечание = пПримечание;
		
	ИначеЕсли ТипЗнч(пПримечание) = Тип("Структура") Или ТипЗнч(пПримечание) = Тип("Соответствие") Тогда 
		стрПримечание = "";
		Для Каждого Пара Из пПримечание Цикл
			стрПримечание = стрПримечание + СокрЛП(Пара.Ключ);
			
			стрЗначение = СокрЛП(Пара.Значение);
			
			Если Не ПустаяСтрока(стрЗначение) Тогда
				стрПримечание = стрПримечание + "=" + стрЗначение;
				
			КонецЕсли;
			
			стрПримечание = стрПримечание + "; ";
			
		КонецЦикла;
		Пара = Неопределено;
		
		стрПримечание = СокрЛП(стрПримечание);
		
	ИначеЕсли ТипЗнч(пПримечание) = Тип("Неопределено") Тогда
		стрПримечание = Неопределено;
		
	Иначе
		стрПримечание = Строка(пПримечание);
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВходящееСообщение", пВходящееСообщениеСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Т.Статус КАК Статус,
	|	Т.Примечание КАК Примечание,
	|	Т.КоличествоОшибок КАК КоличествоОшибок
	|ИЗ
	|	РегистрСведений.обм_ВходящиеСообщения_Статусы.СрезПоследних(, ВходящееСообщение = &ВходящееСообщение) КАК Т";
	
	Выборка = Запрос.Выполнить().Выбрать();
	ТекущийСтатус = Неопределено;
	ТекущееПримечание = Неопределено;
	ТекущееКоличествоОшибок = 0;
	Если Выборка.Следующий() Тогда
		ТекущийСтатус = Выборка.Статус;
		ТекущееПримечание = Выборка.Примечание;
		ТекущееКоличествоОшибок = Выборка.КоличествоОшибок;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущийСтатус) Тогда
		ТекущийСтатус = Неопределено;
		
	КонецЕсли;

	// Если текущий статус совпадает с устанавливаемым, прервать обработку
	Если ТекущийСтатус = пСтатус И ТекущееПримечание = стрПримечание И ТекущееКоличествоОшибок = пКоличествоОшибок Тогда
		Возврат;
		
	КонецЕсли;
	
	// При штатном течении процесса обработки
	// При установке статуса Обработан после статуса ЖдетОбработки
	// Если Примечание не заполнено
	// Взять текущее примечание статуса
	Если 
			пСтатус = Перечисления.обм_ВходящиеСообщения_Статусы.Обработан
		И	ПустаяСтрока(стрПримечание)
		И	ТекущийСтатус = Перечисления.обм_ВходящиеСообщения_Статусы.ЖдетОбработки
	Тогда
		стрПримечание = ТекущееПримечание; 

	// При штатном переходе между статусами добавить новое примечание к старому
	// ЖдетОбработки —> НеОбрабатывается
	// ЖдетОбработки —> Обработан
	// ЖдетОбарботки —> Обработан с ошикой
	ИначеЕсли
			ТекущийСтатус = Перечисления.обм_ВходящиеСообщения_Статусы.ЖдетОбработки
		И   (
					пСтатус = Перечисления.обм_ВходящиеСообщения_Статусы.НеОбрабатывается
				Или	пСтатус = Перечисления.обм_ВходящиеСообщения_Статусы.Обработан
				Или пСтатус = Перечисления.обм_ВходящиеСообщения_Статусы.ОбработанСОшибкой
				)
	Тогда
		стрПримечание = СокрЛП(ТекущееПримечание + " " + стрПримечание);
		
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.обм_ВходящиеСообщения_Статусы.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ВходящееСообщение = пВходящееСообщениеСсылка;
	МенеджерЗаписи.Статус = пСтатус;
	МенеджерЗаписи.Примечание = стрПримечание;
	МенеджерЗаписи.Период = общ.ДатаУниверсальная();
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

Функция Параметр_Значение(пВходящееСообщениеСсылка, КлючПараметра) Экспорт
	
	Для Каждого СтрокаТЧ Из пВходящееСообщениеСсылка.Параметры Цикл
		Если ВРЕГ(СтрокаТЧ.Ключ) = ВРЕГ(КлючПараметра) Тогда
			Возврат СтрокаТЧ.Значение;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Null;
	
КонецФункции  

Функция Параметр_Существует(пВходящееСообщениеСсылка, КлючПараметра) Экспорт
	
	Возврат Справочники.обм_ВходящиеСообщения.Параметр_Значение(пВходящееСообщениеСсылка, КлючПараметра) <> Null	
	
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс

#Область ПрограммныйИнтерфейс

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныйПрограммныйИнтерфейс

Функция ВходящиеСообщения_ПроверитьСортировать(пВходящиеСообщения) Экспорт

	входящиеСообщения = Новый Массив;
	Если ТипЗнч(пВходящиеСообщения) = Тип("СправочникСсылка.обм_ВходящиеСообщения") Тогда
		входящиеСообщения.Добавить(пВходящиеСообщения);
		
	ИначеЕсли ТипЗнч(пВходящиеСообщения) = Тип("Массив") Тогда
		Для Каждого ВходящееСообщениеСсылка Из пВходящиеСообщения Цикл
			Если ТипЗнч(ВходящееСообщениеСсылка) = Тип("СправочникСсылка.обм_ВходящиеСообщения") Тогда
				входящиеСообщения.Добавить(ВходящееСообщениеСсылка);
				
			КонецЕсли;
			
		КонецЦикла;
		ВходящееСообщениеСсылка = Неопределено;
				
	КонецЕсли;

	// Входящие запросы нужно обрабатывать в режиме ФИФО
	// Самый поздний запрос обрабатывается первым, потому что там самые свежие данные
	Если входящиеСообщения.Количество() > 1 Тогда
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("входящиеСообщения", входящиеСообщения);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Т.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.обм_ВходящиеСообщения КАК Т
		|ГДЕ
		|	Т.Ссылка В(&входящиеСообщения)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Т.ДатаВремя УБЫВ";
		
		входящиеСообщения = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");	
		
	КонецЕсли;

	Возврат входящиеСообщения;
	
КонецФункции

#КонецОбласти // СлужебныйПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

Функция СозданиеДанныхЗаполненияРазрешено(пВходящееСообщение)
	
	Если Не общ.ЭтоЗаполненнаяСсылка(пВходящееСообщение, Тип("СправочникСсылка.обм_ВходящиеСообщения")) Тогда
		Возврат Ложь;
		
	КонецЕсли;
	
	текущийСтатус = Справочники.обм_ВходящиеСообщения.Статус(пВходящееСообщение);
	
	обработкаРазрешена = 
			текущийСтатус = Перечисления.обм_ВходящиеСообщения_Статусы.ЖдетОбработки
		Или текущийСтатус = Перечисления.обм_ВходящиеСообщения_Статусы.Обработан 
		;
	
	Возврат обработкаРазрешена;
	
КонецФункции

Процедура ДополнитьПримечание(пПримечание, пДополнение)
	
	пПримечание = СокрЛП(пПримечание + Символы.ПС + пДополнение);
	
КонецПроцедуры

Процедура Ошибка_ВЖурнал(
	пВходящееСообщениеСсылка, 
	пИнфоОшибки, 
	пОбработчикСсылка = Неопределено, 
	пПакетДанных = Неопределено,
	пИмяСобытия = ""
	)

	кратко = ""; 
	подробно = "";
	
	Если ТипЗнч(пИнфоОшибки) = Тип("ИнформацияОбОшибке") Тогда
		кратко = общ.Ошибка_КраткоеПредставление(пИнфоОшибки);
		подробно = общ.Ошибка_ПодробноеПредставление(пИнфоОшибки);
		
	ИначеЕсли ТипЗнч(пИнфоОшибки) = Тип("Строка") Тогда
		кратко = СокрЛП(пИнфоОшибки);
		подробно = кратко;
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(кратко) Тогда
		Справочники.обм_ВходящиеСообщения.Статус_Установить( // ОбработанСОшибкой
			пВходящееСообщениеСсылка, 
			Перечисления.обм_ВходящиеСообщения_Статусы.ОбработанСОшибкой,
			кратко
			);
			
	КонецЕсли;

	Если ПустаяСтрока(подробно) Тогда
		Возврат;
		
	КонецЕсли;
	
	имяСобытия = обм.ИмяСобытияЖурналаРегистрации(пИмяСобытия);
	ссылкиДанных = Новый Массив;
	ссылкиДанных.Добавить(пВходящееСообщениеСсылка);
	ссылкиДанных.Добавить(пОбработчикСсылка);
	ссылкиДанных.Добавить(пПакетДанных);
	
	общ.Ошибка_ВЖурналРегистрации(имяСобытия, подробно, ссылкиДанных);
	
КонецПроцедуры

Процедура ПометитьНаУдалениеПодчиненныеДанныеЗаполнения(пВходящееСообщение)
	
	запрос = Новый Запрос;
	запрос.УстановитьПараметр("ВходящееСообщениеСсылка", пВходящееСообщение);
	запрос.Текст = 
	"ВЫБРАТЬ
	|	Т.Ссылка КАК ДанныеЗаполненияСсылка
	|ИЗ
	|	Справочник.обм_ПакетыДанных КАК Т
	|ГДЕ
	|	Т.Владелец = &ВходящееСообщениеСсылка
	|	И Т.Родитель = ЗНАЧЕНИЕ(Справочник.обм_ПакетыДанных.ПустаяСсылка)
	|	И Т.ПометкаУдаления = ЛОЖЬ";
	
	выборка = запрос.Выполнить().Выбрать();
	Пока выборка.Следующий() Цикл
		данныеЗаполнения = выборка.ДанныеЗаполненияСсылка.ПолучитьОбъект(); 
		
		// Иногда бывает что ссылка битая, это кодгда получают подчиненные элементы справочника
		// Подчиненные элементы справочника содержат данные, которые должны быть записаны в родительский справочник
		// И после этого они будут удалены. Это может произойти параллельно с это обработкой, тогда данныеЗаполнения = Неопределено
		Если данныеЗаполнения = Неопределено Тогда
			Продолжить;
			
		КонецЕсли;
		
		данныеЗаполнения.ПометкаУдаления = Истина;
		данныеЗаполнения.Записать();
		данныеЗаполнения = Неопределено;
		
	КонецЦикла;
	выборка = Неопределено;

КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции

#КонецЕсли // #Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


