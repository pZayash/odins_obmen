
&НаСервере
Процедура ЗапуститьОчередьНаСервере()

	обм.Получение_ОбработатьОчередь();

КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьОчередь(Команда)
	
	ЗапуститьОчередьНаСервере();
	
	ПодключитьОбработчикОжидания("ВходящиеСообщения_ИзмененыСтатусы", 3, Истина);	
	
КонецПроцедуры

&НаКлиенте
Процедура ВходящиеСообщения_ИзмененыСтатусы()
	
	Оповестить("ВходящиеСообщения_ИзмененыСтатусы");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ВходящиеСообщения_ИзмененыСтатусы" Тогда
		УправлениеДиалогом();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Форма списка как форма выбора
	// https://infostart.ru/1c/articles/549160/
		// Элементы.Список - основной реквизит с динамическим списком
		Элементы.Список.РежимВыбора = Параметры.РежимВыбора;
		Если Параметры.МножественныйВыбор <> Неопределено Тогда
			Элементы.Список.МножественныйВыбор = Параметры.МножественныйВыбор;
		КонецЕсли;
		// обход автоматического сохранения пользовательских настроек для разных режимов, спасибо @stolya
		Если Параметры.РежимВыбора И Не ЗначениеЗаполнено(Параметры.КлючПользовательскихНастроек) Тогда
			Параметры.КлючПользовательскихНастроек = "РежимВыбора";
			Список.АвтоматическоеСохранениеПользовательскихНастроек = Ложь;
		КонецЕсли;  

	НастройкиОтбораУстановить();
		
КонецПроцедуры
	
&НаСервере
Процедура НастройкиОтбораУстановить() 
	
	Список.Параметры.УстановитьЗначениеПараметра("ОтборСтатус", ОтборСтатус);
	УправлениеДиалогом();
	
КонецПроцедуры

&НаСервере
Процедура НастройкиОтбораПриИзмененииНаСервере() 
	
	НастройкиОтбораУстановить();
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиОтбораПриИзменении(Элемент) 
	
	НастройкиОтбораПриИзмененииНаСервере();

КонецПроцедуры

&НаСервере
Процедура УправлениеДиалогом()
	
	Если Элементы.Список.Видимость Тогда
		Элементы.Список.Обновить();
		
	КонецЕсли; 
	
	Если Элементы.ФормаПереключитьРегламентное.Видимость Тогда
		регламентное1 = 
			РегламентныеЗадания.НайтиПредопределенное(
				Метаданные.РегламентныеЗадания.обм_Получение_1_ПакетыДанных_Извлечь
				);
		регламентное2 = 
			РегламентныеЗадания.НайтиПредопределенное(
				Метаданные.РегламентныеЗадания.обм_Получение_2_ПакетыДанных_Обработать
				);
		
		заголовокКнопки = Неопределено;
		картинкаКнопки = Неопределено;
		Если регламентное1.Использование И регламентное2.Использование Тогда
			заголовокКнопки = "Задание работает";
			картинкаКнопки = БиблиотекаКартинок.ОформлениеКругЗеленый;
			
		ИначеЕсли Не регламентное1.Использование И Не регламентное2.Использование Тогда
			заголовокКнопки = "Задание отключено";
			картинкаКнопки = БиблиотекаКартинок.ОформлениеКругПустой;
			
		Иначе
			заголовокКнопки = "Задание частично отключено";
			картинкаКнопки = БиблиотекаКартинок.ОформлениеКругЖелтый;
			
		КонецЕсли;

		Элементы.ФормаПереключитьРегламентное.Заголовок = заголовокКнопки;
		Элементы.ФормаПереключитьРегламентное.Картинка = картинкаКнопки;
					
	КонецЕсли; 		

КонецПроцедуры

&НаСервере
Процедура ПереключитьРегламентноеНаСервере()
	
	регламентное1 = 
		РегламентныеЗадания.НайтиПредопределенное(
			Метаданные.РегламентныеЗадания.обм_Получение_1_ПакетыДанных_Извлечь
			);
	регламентное2 = 
		РегламентныеЗадания.НайтиПредопределенное(
			Метаданные.РегламентныеЗадания.обм_Получение_2_ПакетыДанных_Обработать
			);
	
	использование = регламентное1.Использование Или регламентное2.Использование;
	
	регламентное1.Использование = Не использование;
	регламентное1.Записать();
	
	регламентное2.Использование = Не использование;
	регламентное2.Записать(); 
	
	УправлениеДиалогом();

КонецПроцедуры

&НаКлиенте
Процедура ПереключитьРегламентное(Команда)
	ПереключитьРегламентноеНаСервере();
КонецПроцедуры   

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ТипЗнч(Поле) = Тип("ПолеФормы") И Поле.Имя = "ПоследнийПриемник" Тогда
		СтандартнаяОбработка = Ложь;
		
		последнийПриемникСсылка = СписокВыбор_СсылкаПоследнегоПриемникаНаСервере(Элемент.ТекущиеДанные.ПоследнийПриемник);
		ПоказатьЗначение(,последнийПриемникСсылка);

	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция СписокВыбор_СсылкаПоследнегоПриемникаНаСервере(пПоследнийПриемникСтрВнутр)
	
	Возврат ЗначениеИзСтрокиВнутр(пПоследнийПриемникСтрВнутр);	

КонецФункции

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	Для Каждого строкаДинСписка Из Строки Цикл
		Если ПустаяСтрока(строкаДинСписка.ПоследнийПриемник) Тогда
			Возврат;
			
		КонецЕсли;
		
		последнийПриемникСсылка = ЗначениеИзСтрокиВнутр(строкаДинСписка.ПоследнийПриемник);
		
		оформлениеПП = строкаДинСписка.Оформление.ПоследнийПриемник;
		оформлениеПП.УстановитьЗначениеПараметра("Текст", Строка(последнийПриемникСсылка));
		
	КонецЦикла;
	строкаДинСписка = Неопределено;
	
КонецПроцедуры
