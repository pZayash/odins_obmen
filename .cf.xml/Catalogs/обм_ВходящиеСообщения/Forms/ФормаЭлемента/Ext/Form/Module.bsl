&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Статусы.Параметры.УстановитьЗначениеПараметра("ВходящееСообщение", Объект.Ссылка);
	Элементы.Статусы.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Статусы.Параметры.УстановитьЗначениеПараметра("ВходящееСообщение", Объект.Ссылка);
	
	ПакетыДанных.Параметры.УстановитьЗначениеПараметра("ВходящееСообщение", Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПакетыДанныхВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.ПакетыДанных_Приемник Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(,Элемент.ТекущиеДанные.Приемник);
		
	КонецЕсли;
	
	Возврат;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ВходящееСообщение_СохранитьВФайл(Команда)
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение); 
	
	Файл_Имя = обм.ВходящееСообщение_Параметр_Значение(Объект.Ссылка, "Файл_Имя");
	Файл_Расширение = обм.ВходящееСообщение_Параметр_Значение(Объект.Ссылка, "Файл_Расширение");
	Content_type = обм.ВходящееСообщение_Параметр_Значение(Объект.Ссылка, "Content-Type");	
	
	ЗаписатьКакДвоичныеДанные = Ложь;
	Если ЗначениеЗаполнено(Файл_Имя) Тогда
		Диалог.ПолноеИмяФайла = Файл_Имя;
		Если Файл_Расширение = ".xls" Тогда
			ЗаписатьКакДвоичныеДанные = Истина;
			
		КонецЕсли;
		
	ИначеЕсли Content_type = "application/json" Или Content_type = "text/json" Тогда 
		Диалог.Расширение = "json";
		
	ИначеЕсли Content_type = "application/xml" Тогда 
		Диалог.Расширение = "xml";
		
	ИначеЕсли Content_type = "application/octet-stream" Тогда 
		Диалог.Расширение = "bin";
		ЗаписатьКакДвоичныеДанные = Истина;
		
	ИначеЕсли СтрНайти(Content_type, "text/plain") Тогда 
		Диалог.Расширение = "txt";
		
	Иначе
		Диалог.Расширение = "txt";
	
	КонецЕсли; 
	
	Если Не ЗначениеЗаполнено(Диалог.ПолноеИмяФайла) Тогда
		Диалог.ПолноеИмяФайла = 
			СтрШаблон(
				"%1.%2",
				Объект.Наименование,
				Диалог.Расширение
				);
		
	КонецЕсли;
		
	Диалог.МножественныйВыбор = Ложь;
	
	ОбещаниеВыбораФайлов = Диалог.ВыбратьАсинх();
	ВыбранныеФайлы = Ждать ОбещаниеВыбораФайлов;
	
	Если ТипЗнч(ВыбранныеФайлы) <> Тип("Массив") Тогда 
		Возврат;
		
	КонецЕсли;
	
	ПутьКФайлу = ВыбранныеФайлы[0];
	
	Если ЗаписатьКакДвоичныеДанные Тогда
		ОбещаниеЗаписи = Base64Значение(Объект.Запрос).ЗаписатьАсинх(ПутьКФайлу);
		
	Иначе
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ТекстовыйДокумент.УстановитьТекст(Объект.Запрос);
		ОбещаниеЗаписи = ТекстовыйДокумент.ЗаписатьАсинх(ПутьКФайлу);
		
	КонецЕсли;
	
	Ждать ОбещаниеЗаписи;
	
КонецПроцедуры
