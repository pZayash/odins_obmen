#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Статусы.Параметры.УстановитьЗначениеПараметра("ВходящееСообщение", Объект.Ссылка);
	
	ПакетыДанных.Параметры.УстановитьЗначениеПараметра("ВходящееСообщение", Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Статусы.Параметры.УстановитьЗначениеПараметра("ВходящееСообщение", Объект.Ссылка);
	Элементы.Статусы.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПакетыДанныхВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.ПакетыДанных_Приемник Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(,Элемент.ТекущиеДанные.Приемник);
		
	КонецЕсли;
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура ВходящееСообщение_СохранитьВФайл(Команда)
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	
	параметрыСообщения = Новый Структура("Файл_Имя, Файл_Расширение, Content-Type");
	параметрыСообщения = ПараметрыВходящегоСообщения(Объект.Ссылка, параметрыСообщения);
	
	ЗаписатьКакДвоичныеДанные = Ложь;
	Если ЗначениеЗаполнено(параметрыСообщения.Файл_Имя) Тогда
		Диалог.ПолноеИмяФайла = параметрыСообщения.Файл_Имя;
		Если параметрыСообщения.Файл_Расширение = ".xls" Тогда
			ЗаписатьКакДвоичныеДанные = Истина;
			
		КонецЕсли;
		
	ИначеЕсли 	параметрыСообщения.Content_type = "application/json" 
				Или параметрыСообщения.Content_type = "text/json" Тогда 
		Диалог.Расширение = "json";
		
	ИначеЕсли параметрыСообщения.Content_type = "application/xml" Тогда 
		Диалог.Расширение = "xml";
		
	ИначеЕсли параметрыСообщения.Content_type = "application/octet-stream" Тогда 
		Диалог.Расширение = "bin";
		ЗаписатьКакДвоичныеДанные = Истина;
		
	ИначеЕсли СтрНайти(параметрыСообщения.Content_type, "text/plain") Тогда 
		Диалог.Расширение = "txt";
		
	Иначе
		Диалог.Расширение = "txt";
	
	КонецЕсли; 
	
	Если Не ЗначениеЗаполнено(Диалог.ПолноеИмяФайла) Тогда
		Диалог.ПолноеИмяФайла = 
			СтрШаблон(
				"%1.%2",
				Объект.Наименование,
				Диалог.Расширение
				);
		
	КонецЕсли;
		
	Диалог.МножественныйВыбор = Ложь;
	
	ОбещаниеВыбораФайлов = Диалог.ВыбратьАсинх();
	ВыбранныеФайлы = Ждать ОбещаниеВыбораФайлов;
	
	Если ТипЗнч(ВыбранныеФайлы) <> Тип("Массив") Тогда 
		Возврат;
		
	КонецЕсли;
	
	ПутьКФайлу = ВыбранныеФайлы[0];
	
	Если ЗаписатьКакДвоичныеДанные Тогда
		ОбещаниеЗаписи = Base64Значение(Объект.Запрос).ЗаписатьАсинх(ПутьКФайлу);
		
	Иначе
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ТекстовыйДокумент.УстановитьТекст(Объект.Запрос);
		ОбещаниеЗаписи = ТекстовыйДокумент.ЗаписатьАсинх(ПутьКФайлу);
		
	КонецЕсли;
	
	Ждать ОбещаниеЗаписи;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПараметрыВходящегоСообщения(пВходящееСсылка, пИменаПараметров)
	
	Возврат Справочники.обм_ВходящиеСообщения.Параметры(пВходящееСсылка, пИменаПараметров);
	
КонецФункции

#КонецОбласти