&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	пАктивноеОкно = АктивноеОкно();
	
	текущаяФорма = Неопределено;
	Если ТипЗнч(пАктивноеОкно) = Тип("ОкноКлиентскогоПриложения") И пАктивноеОкно.Содержимое.Количество() Тогда
		текущаяФорма = пАктивноеОкно.Содержимое[0];
		
	КонецЕсли;
	
	сборщик = Новый Структура("ИмяФормы, Объект, ТекущийЭлемент");
	
	Если текущаяФорма <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(сборщик, текущаяФорма);
		
	КонецЕсли;
	
	параметрыИсточника = Новый Структура;
	параметрыИсточника.Вставить("ИмяФормы", сборщик.ИмяФормы);
	
	выделенныеСтроки = Неопределено;
	источник = Неопределено;
	Если ТипЗнч(сборщик.ТекущийЭлемент) = Тип("ТаблицаФормы") Тогда
		выделенныеСтроки = текущаяФорма.ТекущийЭлемент.ВыделенныеСтроки;
		текСтрокаТаблицы = текущаяФорма.ТекущийЭлемент.ТекущаяСтрока;
		
		//переопределим Источник, когда основная таблица не указана
		Если ТипЗнч(текСтрокаТаблицы) = Тип("Число") Тогда
			источник = текущаяФорма.ТекущийЭлемент.ДанныеСтроки(текСтрокаТаблицы).Ссылка;
			
		Иначе
			источник = текСтрокаТаблицы;
			
		КонецЕсли;
		
	ИначеЕсли сборщик.Объект <> Неопределено Тогда
		источник = сборщик.Объект.Ссылка;
		
	КонецЕсли;
	параметрыИсточника.Вставить("ВыделенныеСтроки", выделенныеСтроки);
	параметрыИсточника.Вставить("Источник", источник);
	
	форма = ОткрытьФорму("ОбщаяФорма.обм_ПанельКоманд",
			параметрыИсточника,
			ПараметрыВыполненияКоманды.Источник,
			ПараметрыВыполненияКоманды.Уникальность,
			ПараметрыВыполненияКоманды.Окно,
			ПараметрыВыполненияКоманды.НавигационнаяСсылка,
			,
			РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс		);
			
	форма.ПараметрыВыполненияКоманды_Установить(ПараметрыВыполненияКоманды);

КонецПроцедуры
